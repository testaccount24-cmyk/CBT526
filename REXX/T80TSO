/** REXX THE WONDER DOGG                                 **/
/**********************************************************/
/**********************************************************/
/** EXEC NAME        : T80TSO                            **/
/** LAST MODIFIED    : 21 SEP 98                         **/
/**********************************************************/
/**********************************************************/
/** CHANGED TO MAKE MORE GENERIC                         **/
/**********************************************************/
/** CHANGED TO ALLOW DEFINITION OF UNOWNED RESOURCES     **/
/**********************************************************/
/**----------------INPUT REQUIRED -----------------------**/
/** VAR                VALID VALUES                      **/
/** ACTION  : DEFAULTS ADDPROC ADDACCT DEFACCT DEFPROC   **/
/**           DEFSIZE  DEFUNIT MAXSIZE REMACCT TSOAUTH   **/
/** ACID    : ANY VALID TSS ACID                         **/
/** LPROC   : ANY VALID PREDEFINED (TO TSS) PROC         **/
/** ACCTNUM : ANY VALID PREDEFINED (TO TSS) ACCOUNT      **/
/** UNIT    : ANY VALID UNIT                             **/
/** SIZE    : ANY VALID SIZE                             **/
/**                                                      **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00 08 16                     **/
/** ISPF VARIABLES        - SECRC                        **/
/**                                                      **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES                             **/
/** ISPF ENVIRONMENT   : NOT NEEDED, BUT WILL USE        **/
/** EXECS CALLED WITHIN THIS EXEC : T80INS               **/
/**                                                      **/
/**********************************************************/
SYSENV = SYSVAR(SYSENV)
MSG2 = "NO UNUSUAL CONDITIONS WERE FOUND."
ARG ACTION ACID LPROC ACCTNUM UNIT SIZE
/*                                             */
IF SYSENV = "FORE" THEN DO
  ADDRESS ISPEXEC "VGET AUTHNUM PROFILE"
  ADDRESS ISPEXEC "VGET BOOKSEC PROFILE"
END
ELSE DO
  AUTHNUM = 10
  BOOKSEC = "NO"
END
/*IF SUBSTR(ACID         ,8,1) = "@" THEN DO                      */
/*  MSG2 = "WARNING: THE ID YOU HAVE GIVEN ME IS A GHOST ID."     */
/*  IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT MSG2 PROFILE"   */
/*  ELSE SAY SECRC                                                */
/*END                                                             */
IF (LENGTH(ACID) > 8) & ( SUBSTR(ACID        ,8,1) ¬= "@" ) THEN DO
  SECRC = "THE ID YOU HAVE GIVEN ME IS TOO LONG MAX IS 7 CHRS"
  IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  ELSE SAY SECRC
  EXIT 00
END
SIGNAL ON ERROR NAME NOTSSID
Q = OUTTRAP(DROPIT.,5)
ADDRESS TSO "TSS LIST("ACID") DATA(NAME)"
SIGNAL OFF ERROR
X5 = "OK"
SAY "T80TSO: RECEIVED  CONTROL ACTION=" ACTION "ACID="ACID
SAY "T80TSO: PROC="LPROC" ACCT="ACCTNUM" UNIT="UNIT "SIZE="SIZE
/**************************************************/
/* IF THE RESOURCE IS NOT OWNED ADD IT TO TSODEPT */
/**************************************************/
IF (AUTHNUM > 8) & (ACTION ¬= "TSOAUTH")  THEN DO
  Q = OUTTRAP(DROPOUT.,5)
  /* SAY "T80TSO: CHECKING THAT ALL RESOURCES ARE OWNED " */
  TSS_RESOURCE = LPROC
  ADDRESS TSO " TSS WHOOWNS TSOPROC("LPROC")"
  IF RC ¬= 0 THEN DO
    SAY "T80TSO: I AM ADDING THE LPROC TO TSODEPT"
    ADDRESS TSO " TSS ADD(TSODEPT) TSOPROC("LPROC")"
  END
  TSS_RESOURCE = ACCTNUM
  ADDRESS TSO " TSS WHOOWNS TSOACCT("ACCTNUM")"
  IF RC ¬= 0 THEN DO
    SAY "T80TSO: I AM ADDING THE ACCTNUM TO TSODEPT"
    ADDRESS TSO " TSS ADD(TSODEPT) TSOACCT("ACCTNUM")"
  END
  TSS_RESOURCE = "JCL"
  ADDRESS TSO " TSS WHOOWNS TSOAUTH(JCL)"
  IF RC ¬= 0 THEN DO
    SAY "T80TSO: I AM ADDING THE JCL AUTH TO TSODEPT"
    ADDRESS TSO " TSS ADD(TSODEPT) TSOAUTH(JCL)"
  END
  TSS_RESOURCE = "OPER"
  ADDRESS TSO " TSS WHOOWNS TSOAUTH(OPER)"
  IF RC ¬= 0 THEN DO
    SAY "T80TSO: I AM ADDING THE OPER AUTH TO TSODEPT"
    ADDRESS TSO " TSS ADD(TSODEPT) TSOAUTH(OPER)"
  END
  TSS_RESOURCE = "ACCT"
  ADDRESS TSO " TSS WHOOWNS TSOAUTH(ACCT)"
  IF RC ¬= 0 THEN DO
    SAY "T80TSO: I AM ADDING THE ACCT AUTH TO TSODEPT"
    ADDRESS TSO " TSS ADD(TSODEPT) TSOAUTH(ACCT)"
  END
  SAY "T80TSO: RESOURCE OWNER CHECKING COMPLETED."
END
/**************************************************/
/* END OF OWNERSHIP RESOLUTION.                   */
/**************************************************/
Q = OUTTRAP(OUT.,5)
SIGNAL ON ERROR NAME SEEYA
/* ACTION =DEFAULTS */
IF (ACTION = "DEFAULTS") | (ACTION = "DEFTSO") THEN DO
  SAY "T80TSO: ATTEMPTING TO DEFINE ALL TSO AUTHS"
  SAY "T80TSO: REMOVING ANY OLD PERMISSIONS FOR" LPROC ACCTNUM
  IF ACCTNUM ¬= "NIL" THEN DO
    IF BOOKSEC = "YES" THEN
      X5 = TSSCHECK("BOOKNUM",ACCTNUM,"UPDATE")  /* CAN THEY DO THIS*/
    ELSE X5 = "OK"
  END
  SAY "T80TSO: TSSCHECK RETURN CODE IS " X5
  IF X5 ¬= "OK"  THEN SIGNAL BOOKAUTH
  SIGNAL OFF ERROR
  ADDRESS TSO " TSS REV("ACID") TSOPROC("LPROC")"
  ADDRESS TSO " TSS REV("ACID") TSOACCT("ACCTNUM")"
  ADDRESS TSO " TSS REV("ACID") TSOAUTH(JCL)"
  ADDRESS TSO " TSS REM("ACID") TSOLPROC("LPROC")"
  ADDRESS TSO " TSS REM("ACID") TSOLACCT("ACCTNUM")"
  ADDRESS TSO " TSS REM("ACID") TSOLSIZE("SIZE")"
  SAY "T80TSO: DEFINING NEW TSO DEFAULTS."
  SIGNAL ON ERROR NAME NOTSSDEF
  Q = OUTTRAP(OUT.,5)
  TSS_RESOURCE = "LOGON PROC" LPROC
  ADDRESS TSO " TSS PER("ACID") TSOPROC("LPROC")"
  TSS_RESOURCE = "ACCT NUM" ACCTNUM
  ADDRESS TSO " TSS PER("ACID") TSOACCT("ACCTNUM")"
  TSS_RESOURCE = "JCL"
  ADDRESS TSO " TSS PER("ACID") TSOAUTH(JCL)"
  SIGNAL ON ERROR NAME SEEYA
  Q = OUTTRAP(OUT.,5)
  ADDRESS TSO " TSS ADD("ACID") TSOUNIT("UNIT")"
  ADDRESS TSO " TSS ADD("ACID") TSOLPROC("LPROC")"
  IF ACCTNUM ¬= "NIL" THEN
    ADDRESS TSO " TSS ADD("ACID") TSOLACCT("ACCTNUM")"
  ADDRESS TSO " TSS ADD("ACID") TSOLSIZE("SIZE")"
  SAY "T80TSO: CALLING T80INS FOR PROFILE INSERTION"
  ADDRESS TSO "%T80INS" ACID "TSOPROF 9"
  SAY  "T80TSO: BACK FROM T80INS WITH RC OF" RC
END
IF ACTION = "ADDPROC" THEN DO
  SAY "T80TSO: REVOKING ID FROM PROC" LPROC
  SIGNAL OFF ERROR
  ADDRESS TSO "TSS REV("ACID") TSOPROC("LPROC")"
  SIGNAL ON ERROR NAME SEEYA
  SAY "T80TSO: PERMITTING ID TO PROC" LPROC
  ADDRESS TSO "TSS PER("ACID") TSOPROC("LPROC")"
END
IF ACTION = "ADDACCT" THEN DO
  SAY "T80TSO: REVOKING ID FROM ACCT" ACCTNUM
  SIGNAL OFF ERROR
  ADDRESS TSO " TSS REV("ACID") TSOACCT("ACCTNUM")"
  SIGNAL ON ERROR NAME NOBOOK
  SAY "T80TSO: PERMITTING ID TO ACCT" ACCTNUM
  IF BOOKSEC = "YES" THEN
    X5 = TSSCHECK("BOOKNUM",ACCTNUM,"UPDATE")  /* CAN THEY DO THIS */
  ELSE X5 = "OK"
  SAY "T80TSO: TSSCHECK RETURN CODE IS " X5
  IF X5 ¬= "OK"  THEN SIGNAL BOOKAUTH
  ADDRESS TSO " TSS PER("ACID") TSOACCT("ACCTNUM")"
  SIGNAL ON ERROR NAME SEEYA
END
IF ACTION = "DEFACCT" THEN DO
  SIGNAL OFF ERROR
  ADDRESS TSO "%T80GDAT " ACID "TSOLACCT  TSO"
  ADDRESS ISPEXEC "VGET SECRC PROFILE"
  BOOK = SECRC
  IF BOOK = "" THEN SAY "T80TSO: NO DEFAULT BOOK NUMBER DEFINED "
  IF BOOK = "" THEN BOOK  = "$NOTFOUND"
  /********************************************************/
  /* DOES HE HAVE A CURRENT DEFAULT TSS BOOK NUMBER ??    */
  /********************************************************/
  IF (BOOK = "$BADTSS") |  (BOOK = "$NOTFOUND") THEN NOP
  ELSE DO
    /********************************************************/
    /* CAN HE OVERLAY THE CURRENT DEFAULT BOOK NUMBER ??    */
    /********************************************************/
    BOOK = SECRC
    IF BOOKSEC = "YES" THEN
      X5 = TSSCHECK("BOOKNUM",BOOK,"UPDATE")  /* CAN THEY DO THIS */
    ELSE X5 = "OK"
    SAY "T80DEL: TSSCHECK RETURN CODE IS " X5
    IF X5 ¬= "OK"  THEN SIGNAL BOOKREMA
  END
  IF BOOKSEC = "YES" THEN
    X5 = TSSCHECK("BOOKNUM",ACCTNUM,"UPDATE")  /* CAN THEY DO THIS */
  ELSE X5 = "OK"
  SAY "T80TSO: TSSCHECK RETURN CODE IS " X5
  IF X5 ¬= "OK"  THEN SIGNAL BOOKAUTH
  SAY "T80TSO: REVOKING ACCT" ACCTNUM
  ADDRESS TSO " TSS REV("ACID") TSOACCT("ACCTNUM")"
  SIGNAL ON ERROR NAME NOBOOK
  SAY "T80TSO: PERMITTING ACCT" ACCTNUM
  ADDRESS TSO " TSS PER("ACID") TSOACCT("ACCTNUM")"
  SAY "T80TSO: MAKING IDS DEFAULT ACCT" ACCTNUM
  ADDRESS TSO " TSS ADD("ACID") TSOLACCT("ACCTNUM")"
  SIGNAL ON ERROR NAME SEEYA
END
IF ACTION = "DEFPROC" THEN DO
  SIGNAL OFF ERROR
  SAY "T80TSO: REVOKING LOGON PROC" LPROC
  ADDRESS TSO " TSS REV("ACID") TSOPROC("LPROC")"
  SIGNAL ON ERROR NAME SEEYA
  SAY "T80TSO: PERMITTING LOGON PROC" LPROC
  ADDRESS TSO " TSS PER("ACID") TSOPROC("LPROC")"
  SAY "T80TSO: MAKING IDS DEFAULT LOGON PROC" LPROC
  ADDRESS TSO " TSS ADD("ACID") TSOLPROC("LPROC")"
  PDSN = "'MVS.TSO.PROCLIB("LPROC")'"
  /* SAY SYSDSN(PDSN) */
  IF SYSDSN(PDSN)  = "MEMBER NOT FOUND" THEN DO
    MSG2 = "LOGON PROC CHANGED, BUT "PDSN" WAS NOT FOUND"
    IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
    SAY "T80TSO:" MSG2
  END
END
IF ACTION = "DEFSIZE" THEN DO
  SAY "T80TSO: MAKING IDS DEFAULT LOGON SIZE" SIZE
  ADDRESS TSO " TSS ADD("ACID") TSOLSIZE("SIZE")"
END
IF ACTION = "DEFUNIT" THEN DO
  SAY "T80TSO: MAKING IDS DEFAULT LOGON UNIT" UNIT
  ADDRESS TSO " TSS ADD("ACID") TSOUNIT("UNIT")"
END
IF ACTION = "MAXSIZE" THEN DO
  SAY "T80TSO: MAKING IDS MAXIMUM REGION SIZE" SIZE
  ADDRESS TSO " TSS ADD("ACID") TSOMSIZE("SIZE")"
END
IF ACTION = "REMACCT" THEN DO
  SAY "T80TSO: REVOKING ID FROM ACCOUNT " ACCTNUM
  IF BOOKSEC = "YES" THEN
    X5 = TSSCHECK("BOOKNUM",ACCTNUM,"UPDATE")  /* CAN THEY DO THIS */
  ELSE X5 = "OK"
  SAY "T80TSO: TSSCHECK RETURN CODE IS " X5
  IF X5 ¬= "OK"  THEN SIGNAL BOOKAUTH
  SIGNAL OFF ERROR
  ADDRESS TSO " TSS REV("ACID") TSOACCT("ACCTNUM")"
  SIGNAL ON ERROR NAME SEEYA
END
IF ACTION = "REMPROC" THEN DO
  SAY "T80TSO: REVOKING ID FROM LOGON PROC " LPROC
  SIGNAL OFF ERROR
  ADDRESS TSO " TSS REV("ACID") TSOPROC("LPROC")"
  SIGNAL ON ERROR NAME SEEYA
END
IF ACTION = "TSOAUTH" THEN DO
  SAY "T80TSO: REVOKING ID FROM AUTHS " LPROC
  SIGNAL OFF ERROR
  ADDRESS TSO " TSS REV("ACID") TSOAUTH("LPROC")"
  SIGNAL ON ERROR NAME SEEYA
  SAY "T80TSO: PERMITTING ID TO " LPROC
  ADDRESS TSO " TSS PER("ACID") TSOAUTH("LPROC")"
END
SAY "T80TSO: CHECKING FOR TSO AREA COMPLETENESS."
SAY " "
SIGNAL OFF ERROR
IF (ACTION ¬= "DEFAULTS") & (ACTION ¬= "DEFTSO") THEN DO
  ADDRESS TSO "%T80GDAT "ACID" TSOLACCT TSO"
  IF SYSENV = "FORE" THEN
    ADDRESS ISPEXEC "VGET SECRC PROFILE"
  DEFAULT_ACCT = SECRC
  SAY "T80TSO: IDS DEFAULT ACCOUNT IS " DEFAULT_ACCT
  ADDRESS TSO "%T80GDAT "ACID" TSOLPROC TSO"
  IF SYSENV = "FORE" THEN
    ADDRESS ISPEXEC "VGET SECRC PROFILE"
  DEFAULT_PROC = SECRC
  SAY "T80TSO: IDS DEFAULT LPROC IS " DEFAULT_PROC
  IF (DEFAULT_PROC = "$NOTFOUND") THEN DO
    MSG2 = "ID HAS NO DEFAULT PROC IN TSS,"
    MSG2 = MSG2 "MAY NEED THE DEFTSO CMD ISSUED."
    ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
  END
  IF (DEFAULT_ACCT = "$NOTFOUND") THEN DO
    MSG2 = "ID HAS NO DEFAULT ACCOUNT IN TSS,"
    MSG2 = MSG2 "MAY NEED THE DEFTSO CMD ISSUED."
    ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
  END
END
SAY "T80TSO: FALL THRU AREA REACHED. SETTING MSG AND EXITING"
SAY " "
SECRC = ACTION "FUNCTION COMPLETED"
IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
ELSE SAY SECRC MSG2
EXIT 0
SEEYA: NOP
SAY "T80TSO: **FATAL** ERROR OCCURRED IN LINE :" SIGL
TEMP = SOURCELINE(SIGL)
SAY "T80TSO: LINE" SIGL "=" TEMP
IF RC = 8 THEN DO
  SECRC = "FUNCTION FAILED. CONFIRM VALIDITY (OWNERSHIP) OF DATA"
  IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  SAY "T80TSO: *************************************************"
  SAY "T80TSO: EXITING WITH RC = 08 PROBABLE TSS COMMAND FAILURE"
  SAY "T80TSO: *************************************************"
  EXIT 8
END
NOBOOK: NOP
SAY "T80TSO: ERROR OCCURRED IN LINE :" SIGL
TEMP = SOURCELINE(SIGL)
SAY "T80TSO: LINE" SIGL "=" TEMP
IF RC = 8 THEN DO
  SECRC = "FUNCTION FAILED. BOOK NUMBER MAY NOT EXIST "
  IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  SAY "T80TSO: EXITING WITH RC = 08 PROBABLE TSS OWNERSHIP ERROR."
  EXIT 8
END
SAY "T80TSO: *************************************************"
SAY "T80TSO: EXITING WITH RC = 16 !  FATAL ERROR."
SAY "T80TSO: *************************************************"
EXIT 1
NOTSSDEF: NOP
SAY "T80TSO: A PERMIT ERROR OCCURRED IN LINE:" SIGL
SECRC = "ABORTED." TSS_RESOURCE" IS UNOWNED IN TSS."
IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
SAY OUT.1
SAY OUT.2
SAY OUT.3
SAY "T80TSO: "SECRC
EXIT 8
NOTSSID: NOP
SAY "T80TSO: THE ID YOU HAVE GIVEN DOES NOT EXIST"
SECRC = "ABORTED. NO SUCH TOP SECRET ID, UNDER YOUR OWNERSHIP."
IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
SAY "T80TSO: "SECRC
EXIT 8

BOOKAUTH: NOP
SAY TSSCHECK RCODE WAS X5
SECRC = "YOU ARE NOT AUTHORIZED FOR THIS BOOK FUNCTION. BOOK= " ACCTNUM
IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
SAY "T80TSO: "SECRC
EXIT 8

BOOKREMA: NOP
SAY TSSCHECK RCODE WAS X5
SECRC = "YOU CANT OVERLAY BOOK VALUE = "BOOK". TRY ADDACCT INSTEAD."
IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
SAY "T80TSO: "SECRC
EXIT 8

TSSNOADD: NOP
SAY "T80TSO: AN OWNERSHIP ERROR OCCURRED IN LINE:" SIGL
SECRC = "ABORTED." TSS_RESOURCE" OWNERSHIP RESOLUTION FAILED IN TSS."
IF SYSENV = "FORE" THEN ADDRESS ISPEXEC "VPUT SECRC PROFILE"
SAY "T80TSO: "SECRC
EXIT 8
