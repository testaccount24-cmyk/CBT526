/**REXX THE WONDER DOGG                                  **/
/**********************************************************/
/** V10R1M3 ADDED GENERIC VOLUME PARENTHETICAL SUPPORT   **/
/**********************************************************/
/** ADDED SUPPROT FOR THE LIBRARY PARM ON PERMITS        **/
/**********************************************************/
/** EXEC NAME        : T50RCON                           **/
/**********************************************************/
/** CONVERTS ALL RESOURCE    RULES FOR A SPECIFIC RESOURCE*/
/** DATABASE WIDE. SAME PREMISE AS AN ID CONVERT, BUT THE**/
/** RESOURCE MUST BE SUBJECT TO THE TSS WHOHAS COMMAND   **/
/**********************************************************/
/** LAST MODIFIED    :  5 OCT 98                         **/
/**********************************************************/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00                           **/
/** INPUT PARMS      VALUE                               **/
/** RTYPE            THE TYPE OF THE RESOURCE            **/
/** RNAME            THE NAME OF THE RESOURCE            **/
/** ALTOPT           "    " OR "REMOVE" OR "AUDIT"       **/
/** ISPF VARIABLES        - SECRC                        **/
/**                                                      **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES (NEEDS VGET VARS THOUGH)    **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC : T50JCARD             **/
/**                                                      **/
/**********************************************************/
ARG RTYPE RNAME ALTOPT
MAINT = "V10R1M3"
ADDRESS ISPEXEC
"VGET DS1 PROFILE  "
"VGET DS2 PROFILE  "
"VGET DS3 PROFILE  "
"VGET DS4 PROFILE  "
"VGET DS5 PROFILE  "
"VGET DS6 PROFILE  "
SECRC = "T50RCON RECEIVED CONTROL BUT HAD ERRORS"
"VPUT SECRC PROFILE"
IF RTYPE = "BYPASS" & ALTOPT = "" THEN ALTOPT = RNAME
SAY "T50RCON: RECEIVING CONTROL" MAINT RTYPE RNAME ALTOPT
ADDRESS TSO
OUTNUM = -1
IF ALTOPT = "REMOVE" THEN OUTNUM = 0
UNTIL = ""
SAY "T50RCON: CALLING T50JCARD TO " DS2
ADDRESS TSO "%T50JCARD" DS2 "RANDOM"
SAY "T50RCON: BACK FROM T50JCARD"
SAY "T50RCON: I WILL TRY TO ALLOCATE " DS2
"ALLOC DDN(OUTFILE) DSN(" DS2") MOD REUS"
SAY "T50RCON: I HAVE ALLOCATED " DS2
IF ALTOPT = "REMOVE" THEN
  SAY "T50RCON: REMOVE OPTION HAS BEEN CONFIRMED. "
IF RTYPE = "BYPASS" THEN DO
  SAY "T50RCON: BYPASS PARM DETECTED. ALLOCATING " DS1 "FOR INPUT"
  "ALLOC DDN(INFILE) DSN("DS1") SHR REUS"
  SAY "T50RCON: I HAVE ALLOCATED " DS1 "SUCCESSFULLY"
  "EXECIO * DISKR INFILE (FINIS STEM LINE.)"
  "FREE DDN(INFILE)"
  RTYPE = WORD(LINE.1,1)
  END
ELSE DO
  SAY "T50RCON: ISSUEING THE COMMAND TSS WHOHAS" RTYPE"("RNAME")"
  Q = OUTTRAP("LINE.")
  "TSS WHOHAS "RTYPE"("RNAME")"
END /* ENDIF  */
SAY "T50RCON: I HAVE " LINE.0 "LINES OF DATA TO BE PROCESSED"
DO X = 2 TO LINE.0
  INLINE = LINE.X
  X1 = X + 1
  NEXTLINE = LINE.X1
  /* V1R1M3 */
  /** ONLY CHANGE PARENS WHEN THEY OCCUR AT THE END OF THE RECORD */
  ENDLINE = SUBSTR(INLINE,40)
  BEGLINE = SUBSTR(INLINE,1,39)
  ENDLINE = TRANSLATE(ENDLINE," ",")")
  ENDLINE = TRANSLATE(ENDLINE," ","(")
  INLINE = BEGLINE||ENDLINE
  /* V1R1M3 */
  PARSE VAR INLINE W.1 W.2 W.3 W.4 W.5 W.6 W.7 W.8 W.9 W.10
  IF ACID = "*ALL*" THEN ACID = "ALL"
  IF W.1 = "PRIVPGM" THEN PGM = "PRIVPGM("W.3")"
  IF W.1 = "LIBRARY" THEN LIBRARY = "LIBRARY("W.3")"
  IF (W.1 = "ACTION") & (W.3 = "FAIL,PASSWORD") THEN ITERATE X
  IF W.1 = "RESOURCE" THEN DO
    OUTNUM = OUTNUM + 1 /* A NEW RNAME IS BORN */
    OUTLINE.OUTNUM = " TSS ADD("ACID") "RTYPE"("RNAME")"
    IF ALTOPT = "REMOVE" THEN
      OUTLINE.OUTNUM = " TSS REM("ACID") "RTYPE"("RNAME")"
    ITERATE X
  END
  IF (W.1 = "XAUTH") | (W.1 = "TSS0300I") THEN DO
    SAY RNAME ACID ACCESS UNTIL ACTION FAC PGM LIBRARY
    OUTLINE.OUTNUM = " TSS REV("ACID") "RTYPE"("RNAME")"
    IF ( ALTOPT = "REMOVE" ) | ( ALTOPT = "AUDIT" ) THEN DO
      SAY "T50RCON: SPECIAL PROCESSING REQUESTED, ALTOPT = " ALTOPT
      SAY "T50RCON: OUTLINE IS " OUTLINE.OUTNUM
      OUTNUM = OUTNUM + 1 /* KEEP THE REVOKE   */
    END
    /***************************************************/
    /** KEEP THE ACTION OR OVERLAY IT IF AUDITING     **/
    /***************************************************/
    /***************************************************/
    /** CREATE THE  PERMIT LINE AND THE ACCESS LINE.  **/
    /***************************************************/
    OUTLINE.OUTNUM = " TSS PER("ACID") "RTYPE"("RNAME") +"
    OUTNUM = OUTNUM + 1 /* ACCESS COMING UP  */
    OUTLINE.OUTNUM = "     " ACCESS PGM UNTIL ACTION FAC LIBRARY
    OUTNUM = OUTNUM + 1 /* KEEP WHAT WE HAVE */
    IF (ACCESS = "ACCESS") & (ALTOPT = "AUDIT") THEN
       OUTNUM = OUTNUM - 1 /* FIRST RECORD */
    IF ALTOPT = "REMOVE" THEN DO
      SAY "T50RCON: DISCARDING PERMITS DUE TO ALOTPT SETTING."
      OUTNUM = OUTNUM - 2 /* KILL PERMIT*/
    END
    DISPLAY = OUTNUM - 2
    DISPLAY1 = OUTNUM - 1
    SAY "T50RCON: CREATED OUTLINES ARE" OUTLINE.DISPLAY
    SAY "                            " OUTLINE.DISPLAY1
                 /***************************************************/
    ACCESS = ""  /***************************************************/
    UNTIL  = ""  /* BLANK THESE PUPPIES OUT EACH TIME WE WRITE THEM */
    ACTION = ""  /***************************************************/
    PGM    = ""  /***************************************************/
    LIBRARY = "" /***************************************************/
    FAC    = ""  /***************************************************/
  END
  DO ANUM = 1 TO 7
    NEXTA = ANUM + 1
    NEXTB = ANUM + 2
    IF W.1    = "XAUTH"  THEN RNAME  = W.3
    IF W.1    = "PGM"    THEN PGM    = "PGM("W.3")"
    IF W.1    = "FAC"    THEN FAC    = "FAC("W.3")"
    IF W.ANUM = "UNTIL"  THEN UNTIL  = "UNTIL("W.NEXTA")"
    IF W.ANUM = "ACCESS" THEN ACCESS = "ACCESS("W.NEXTB")"
    IF ALTOPT = "AUDIT"  THEN ACTION = "ACTION(AUDIT)"
    IF W.ANUM = "ACTION" THEN DO
      IF (INDEX(W.NEXTB,"AUDIT") = 0) & (ALTOPT = "AUDIT") THEN,
        W.NEXTB = W.NEXTB||",AUDIT"
      ACTION = "ACTION("W.NEXTB")"
    END
    IF W.ANUM = "ACID"   THEN ACID   =  W.NEXTA
  END
END
OUTLINE.OUTNUM = "@@"   /* JCL DD DLM MARKER     */
SAY "T50RCON: I AM WRITING "OUTNUM " LINES OF OUTPUT"
IF OUTNUM > 1 THEN
  "EXECIO "OUTNUM" DISKW OUTFILE( FINIS STEM OUTLINE.)"
"FREE DDN(OUTFILE)"
SECRC = "CONVERSION STREAM CREATED IN DATASET" DS2
IF OUTNUM < 1 THEN SECRC = "NO OUTPUT GENERATED. CHECK RNAME VALIDITY"
SAY "T50RCON: SECRC IS " SECRC
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
SAY "T50RCON: LEAVING T50RCON"
EXIT 0
