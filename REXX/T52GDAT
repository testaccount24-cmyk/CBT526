/** rexx                                                 **/
/**********************************************************/
/**********************************************************/
/** EXEC NAME        : T52GDAT                           **/
/** LAST MODIFIED    : 02 MAY 96                         **/
/**********************************************************/
/** ADDED SUPPORT FOR MULTIPLE WORD FIELDS AND THE       **/
/** SPECIAL REQUIREMENTS OF THE PROFILE FIELD..DR        **/
/**********************************************************/
/**----------------INPUT REQUIRED -----------------------**/
/** PASSED ARGS        VALID VALUES                      **/
/** ACID    : ANY TSS ACID                               **/
/** FIELD   : THE TSS FIELD THE CALLER WANTS TO RECEIVE  **/
/**           BACK                                       **/
/** DATA    : THE TYPE OF LIST I MUST DO. (NOT REQ)      **/
/** ISPF VGETS         VALID VALUES                      **/
/** NONE                                                 **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00 04 08 16                  **/
/** ISPF VARIABLES        - SECRC                        **/
/**                                                      **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES                             **/
/** ISPF ENVIRONMENT   : YES (VPUT OF SECRC)             **/
/** EXECS CALLED WITHIN THIS EXEC :  N/A                 **/
/**                                                      **/
/**********************************************************/
ARG ACID FIELD DATA
/* SAY ACID FIELD DATA */
HEX00 = '00'X
/** IF THE FIELD REQUESTED IS ONE THAT NEEDS MULTIPLE    **/
/** WORDS PASSED BACK TO THE CALLER THEN SET THE MULT.  **/
/** VARIABLE TO YES BELOW... DIR JAN 97                 **/
MULT.USED = "YES"
MULT.NAME = "YES"
MULT.PROFILES = "YES"
MULT.INSTDATA = "YES"
MULT.BYPASSING = "YES"
MULT.ATTRIBUTES = "YES"
/* SAY "T52GDAT: HAS CONTROL PARMS " ACID FIELD DATA */
GOTIT = "NO"
IF DATA = " " THEN DATA = "BASIC"
SIGNAL ON ERROR NAME BADDATA
Q = OUTTRAP(OUTID.)
ADDRESS TSO "TSS LIST("ACID") DATA("DATA")"
SIGNAL ON ERROR NAME SEEYA
DO X = 1 TO OUTID.0
  X1 = X+1 ; X2 = X+2 ; X3 = X+3 ; X4 = X+4
  /****************************/
  /* REPLACE X'00' WITH X'40' */
  /****************************/
  OUTID.X=TRANSLATE(OUTID.X," ",HEX00)
  PARSE VAR OUTID.X W.1 W.2 W.3 W.4 W.5 W.6 W.7 W.8 W.9 W.10 W.11 W.12
  WNUM = WORDS(OUTID.X)
  DO T = 1 TO WNUM
    IF WORD(OUTID.X,T) = FIELD THEN DO
      /* SAY "T52GDAT: LINE IS " OUTID.X                 */
      /*SAY "T52GDAT: FIELD IS" FIELD                    */
      /*SAY "T52GDAT: GOT IT ON LINE " X " WORD " T      */
      T1 = T + 1
      T2 = T + 2
      T3 = T + 3
      SECRC = W.T2
      /* SAY "T52GDAT: SETTING INITIAL SECRC TO " SECRC    */
      IF FIELD = "DEPT" THEN SECRC = W.T3
      IF (FIELD = "TYPE") & (W.T3 = "C/A") THEN
        SECRC = SECRC||"CA"
 /*   IF FIELD = "NAME" THEN */
 /*     SECRC = SECRC  W.T3  */
      /**************************************************/
      /** BELOW IS SUPPORT FOR A FIELD THAT HAS MULTIPLE*/
      /** PARMS, LIKE PROFILES ...DIR 2 MAY 96          */
      /**************************************************/
      IF MULT.FIELD = "YES" THEN DO
        IF FIELD = "PROFILES" THEN DO
          /**************************************/
          /** IF IT IS A PROFILES REQUEST WE   **/
          /** SCAN AHEAD AND LOAD THE CURRENT  **/
          /** LINE WITH ALL THE PROFILES IN THE**/
          /** NEXT 4 LINES, THUS SUPPORTING 5  **/
          /** FULL LINES OF PROFILES OR 20 OF  **/
          /** THOSE BUGGARS.   DIR 2 MAY 96    **/
          /**************************************/
          /* SAY "T52GDAT: PROFILE ROUTINE IN CONTROL"        */
          NW1 = WORD(OUTID.X1,1)
          NW2 = WORD(OUTID.X2,1)
          NW3 = WORD(OUTID.X3,1)
          NW4 = WORD(OUTID.X4,1)
          IF NW1 = "PROFILES" THEN W.8 = SUBSTR(OUTID.X1,14)
          IF NW2 = "PROFILES" THEN W.9 = SUBSTR(OUTID.X2,14)
          IF NW3 = "PROFILES" THEN W.10 = SUBSTR(OUTID.X3,14)
          IF NW4 = "PROFILES" THEN W.11 = SUBSTR(OUTID.X4,14)
          /**************************************/
          /** LETS TELL THE NEXT ROUTINE THERE **/
          /** ARE MORE WORDS THAN THERE USED TO**/
          /** BE IN THE CURRENT LINE.DR 2 MAY96*/
          /**************************************/
          WNUM = 11
        END
        /*SAY "T52GDAT: MULTIPLE WORD FIELD DETECTED."    */
        /*SAY "T52GDAT: COUNTING FROM "T2 " TO " WNUM     */
        ALLDATA = ""
        DO W1 = T2 TO WNUM
          IF W.W1 = "=" THEN LEAVE /* WE HAVE HIT THE NEXT VALUE */
          ALLDATA = ALLDATA  W.W1
        END
        SECRC = ALLDATA
      END
      GOTIT = "YES"
    END
    IF GOTIT = "YES" THEN LEAVE
  END
  IF GOTIT = "YES" THEN LEAVE
END
SIGNAL OFF ERROR
/*****************************************************************/
/** NOT FINDING AN INTERVAL MAY BE OK PASS BACK A ZERO.         **/
/*****************************************************************/
IF (GOTIT = "NO") & (FIELD = "INTERVAL") & (DATA Â¬= "BASIC") THEN DO
  GOTIT = "YES"
  SECRC = 0
END
IF GOTIT = "YES" THEN DO
  /* SAY "T52GDAT: FIELD "FIELD" WAS FOUND. VALUE IS " SECRC   */
  ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  EXIT
END
SECRC = "$NOTFOUND"
/* SAY "T52GDAT: FIELD "FIELD" WAS NOT FOUND. SECRC IS " SECRC  */
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT 0
BADDATA: NOP
SECRC  = "$BADTSS"
SAY "T52GDAT: ERROR ROUTINE ENTERED, LIST DATA IS" DATA
SAY "T52GDAT: "ACID FIELD DATA
IF WORD(OUTID.1,1) = "TSS0352E" THEN DO
  SECRC = "$BADAUTH"
  SAY "T52GDAT: ID IS NOT OWNED WITHIN SCOPE OF CONTROL"
END
IF WORD(OUTID.1,1) = "TSS0314E" THEN DO
  SECRC = "$BADACID"
  SAY "T52GDAT: No such TSS ACID."
END
SAY "T52GDAT: SECRC IS BEING SET TO " SECRC
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT 0
SEEYA: NOP
SAY "T52GDAT: AN ERROR HAS OCCURED IN LINE "SIGL
EXIT 1
