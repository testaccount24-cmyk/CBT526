/*REXX*/
/** REXX                                                 **/
/**********************************************************/
/** EXEC NAME        : T50BCON                           **/
/** LAST MODIFIED    : 24 SEP 96                         **/
/**----------------INPUT REQUIRED -----------------------**/
/**********************************************************/
/** BASE SUPPORT FOR ZCA TYPE ADMINS                     **/
/**********************************************************/
/** PASSED ARGS        VALID VALUES                      **/
/** THESE ARGS ARE ONLY PASSSED ON A LSCA CONVERT        **/
/** NEWACID : THE NAME THAT THE ACID IS TO BE CHANGED TO **/
/** NEWTYPE : THE TYPE OF ACID THAT IS TO BE CREATED     **/
/** ISPF VGETS         VALID VALUES                      **/
/** DS1     : THE DATASET WHEWRE THE INPUT DATA MUST BE  **/
/** DS2     : THE OUTPUT DATASET                         **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00 08 12                     **/
/** ISPF VARIABLES        - SECRC                        **/
/**                                                      **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES.(NOTE VGET VARIABLES)       **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC : NONE                  */
/**                                                      **/
/**********************************************************/
ARG NEWACID NEWTYPE
SIGNAL ON ERROR NAME SEEYA
SIGNAL ON SYNTAX NAME SYNERR
SECRC = "FUNCTION FAILED IN EXEC T50BCON."
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
ADDRESS ISPEXEC "VGET DS1 PROFILE"
ADDRESS ISPEXEC "VGET DS2 PROFILE"
SAY "T50BCON: TOP SECRET REXX BASIC ACID CONVERSION CLIST ENTERED"
NEST = SYSVAR(SYSNEST)
IF NEST = "NO" THEN DO
  "ALLOC DDN(OUTFILE) DSN(" DS2") MOD REUS"
  "ALLOC DDN(INPUT) DSN(" DS1") SHR REUS"
END
"EXECIO * DISKR INPUT (FINIS STEM INLINE.)"
OUTFILE. = " "
ATTR = "ATTR"
OPID = " "
PWD = "12345"
NEXTREC: DO RECNUM = 1 TO INLINE.0
  PREVLINE=  INLINE
  INLINE= SUBSTR(INLINE.RECNUM,1,59)
  IF INLINE = "      " THEN ITERATE
  PARSE VAR INLINE PAR.1 PAR.2 PAR.3 PAR.4 PAR.5 PAR.6 PAR.7 PAR.8
  IF (PAR.1 = "ACCESSORID") & ( RECNUM > 3 ) THEN DO
    "EXECIO 5 DISKW OUTFILE( FINIS STEM OUTFILE.)"
    OUTFILE.  = "         "
    ATTR = "ATTR"
    TYPE = "TYPE"
    DEPT = "DEPT"
    NAME = "NAME"
    OPID = " "
    INSTDATA = "INSTDATA"
    ACID = "ACID"
  END
  IF PAR.1 = "INSTDATA" THEN DO
     NEXT = RECNUM + 1
/*   INSTDATA ="'"||SUBSTR(INLINE.NEXT,1,55)||"'"    */
     INSTDATA ="'"||SUBSTR(INLINE,14,65)||"'"
     ITERATE
  END
  IF PAR.1 = "OPIDENT" THEN OPID = "OPIDENT("PAR.3")"
  IF PAR.3 = "OPPRTY" THEN OPID = " "
  IF PAR.1 = "PASSWORD" THEN PWD = PAR.3
  IF PWD   = "*NOPW*" THEN PWD = "NOPW"
  IF PAR.3 = "*NONE*" THEN ITERATE
  IF PAR.1 = "ACCESSORID" THEN ACID = PAR.3
  IF NEWACID ¬= " " THEN ACID = NEWACID
  IF ACID = "*STC*" THEN EXIT 00
  IF ACID = "*RDT*" THEN EXIT 00
  IF ACID = "*ALL*" THEN EXIT 00
  IF PAR.1 = "DEPT" THEN DEPT = PAR.4
  IF PAR.1 = "DIV" THEN DIV = PAR.4
  IF PAR.1 = "TYPE" THEN TYPE = PAR.3
  IF NEWTYPE ¬= " " THEN TYPE = NEWTYPE
  IF PAR.4 = "NAME" THEN NAME = STRIP(PAR.6  PAR.7  PAR.8 )
  IF PAR.1 = "ATTRIBUTES" THEN ATTR = PAR.3
  IF (TYPE = "DEPT") & (PAR.4 = "C/A") THEN TYPE = "DCA"
  IF (PAR.1 = "ZONE") & (PAR.2 = "ACID") THEN TZONE = PAR.4
  IF (TYPE = "ZONE") & (PAR.4 = "C/A") THEN TYPE= "ZCA"
  IF TYPE = "LIMITED" THEN TYPE = "LSCA"
  IF TYPE = "MASTER" THEN EXIT
  PRINTIT: IF TYPE = "CENTRAL" THEN TYPE = "SCA"
  IF TYPE = "DIV" THEN TYPE = "VCA"
  OUTFILE.  = "            "
  OUTFILE.1 = " TSS CRE("ACID") NAME('"NAME"') TYPE("TYPE") +"
  OUTFILE.2 ="  DEPT("DEPT")  +"
  IF TYPE = "ZONE" THEN OUTFILE.2 = " "
  IF TYPE = "VCA" THEN
    OUTFILE.2 ="  DIV("DIV") +"
  IF TYPE = "ZCA" THEN
    OUTFILE.2 ="  ZONE("TZONE") +"
  IF TYPE = "DEPT" THEN
    OUTFILE.2 ="  DIV("DIV") +"
  IF (TYPE="SCA") | (TYPE= "LSCA") THEN DO
    OUTFILE.2 ="  PASSWORD("PWD",30,EXP) +"
    IF OPID ¬= " " THEN
      OUTFILE.2 = "  PASSWORD("PWD",30,EXP) "OPID" +"
    OUTFILE.3 = "                       "
    IF INSTDATA ¬= "INSTDATA" THEN
    OUTFILE.3 ="  INSTDATA("INSTDATA") "
    OUTFILE.4 ="                       "
  END
  IF TYPE="PROFILE" THEN DO
    OUTFILE.3 = "                       "
    IF INSTDATA ¬= "INSTDATA" THEN
    OUTFILE.3 = "  INSTDATA("INSTDATA") "
    OUTFILE.4 = "                       "
    OUTFILE.5 = "                       "
  END
  IF (TYPE="USER") | (TYPE="DCA") | (TYPE="VCA") THEN DO
    OUTFILE.3 = " PASSWORD("PWD",30,EXP) "OPID" +"
    OUTFILE.4 = "                       "
    IF INSTDATA ¬= "INSTDATA" THEN
    OUTFILE.4 ="  INSTDATA("INSTDATA") "
  END
  IF ATTR <> "ATTR" THEN
    OUTFILE.5 = " TSS ADD("ACID") "ATTR" "
  OUTFILE.6 = "         "
END
"EXECIO 5 DISKW OUTFILE( FINIS STEM OUTFILE.)"
EXIT 0
SEEYA: NOP
SECRC = "FUNCTION FAILED IN EXEC T50BCON. ERROR IN LINE " SIGL
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT 0
SYNERR: NOP
SECRC = "FUNCTION FAILED IN EXEC T50BCON. SYNTAX ERROR LINE " SIGL
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT 0
