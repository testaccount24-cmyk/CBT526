/* rexx */
LOG:
  message = arg(1)
  parm    = arg(2)
  funct   = arg(3)
  upper funct parm
  if funct = "" then funct = "APPEND"  /* the default option */
  Select
    when funct = "INIT" then do
      upper message
      /* log(debug,100,init) */
      log.message.0max = parm
      if datatype(parm) /= "NUM" then return 4
      return 0
      /* above inits a new log var */
    end
    when funct = "APPEND" then do
      /* log(my log message) */
      /* log(my log message,debug,append) */
      if parm = "" then parm = "DEBUG" /* the default log */
      if datatype(log.parm.0) /= "NUM" then log.parm.0 = 0
      if datatype(log.parm.0max) /= "NUM" then do
        say "LOG002E: Uninitialized log variable"
        say "         You must init a log variable before use."
        return 12
      end
      hold = log.parm.0 + 1
      if hold > log.parm.0max then log.parm.0wrap = "YES"
      if hold > log.parm.0max then hold = 1
      /* if we are at max then start at #1 again */
      log.parm.hold = message
      log.parm.0 = hold        /* update the tracker subscript */
      hold = hold + 1
      log.parm.hold = "_____________"
      return 0
    end
    when funct = "PURGE" then do
      /* log(,debug,purge) */
      /* keep the stream but delete the contents */
      savemax = log.parm.0max
      log.parm. = ""
      log.parm.0max = savemax
      log.parm.0 = 0            /* reset my array */
      return 0
    end
    when funct = "DELETE" then do
      /* log(,debug,delete) */
      /* as if it never existed */
      log.parm. = ""
      return 0
    end
    when funct = "DUMP" then do
      /* log(ddname,debug,dump) */
      ddname = message
      lines = log.parm.0
      stem = "log."parm"."
      savemax = log.parm.0max
      if log.parm.0wrap = "YES" then lines = log.parm.0max
      if datatype(log.parm.0) /= "NUM" then log.parm.0 = 0
      /* above inits a new log var */
      ADDRESS MVS "EXECIO "lines" DISKW "ddname" ( FINIS STEM "stem")"
      log.parm. = ""
      log.parm.0max = savemax
      log.parm.0 = 0            /* reset my array */
      return 0
    end
    WHEN FUNCT = "WRITE" THEN DO
      /* log(ddname,debug,dump) */
      ddname = message
      if datatype(log.parm.0) /= "NUM" then log.parm.0 = 0
      lines = log.parm.0
      stem = "log."parm"."
      if log.parm.0wrap = "YES" then lines = log.parm.0max
      do w = 1 to lines
        say log.parm.w
      end
      return 0
    end
    otherwise return 4
   End
