/** rexx                                                **/
/** sample exec to create a tsscfile                    **/
/*********************************************************/
/** to add this exec to your system include this line   **/
/** in your SRCCMD DSN:                                 **/
/** MAKECFILE %TSSCFILE 1     0     0    5    ON     2  **/
/**                                                     **/
/** Then:                                               **/
/** add the cfileds variable to the T$$.... exec        **/
/** add a vput statement for the cfileds in T$$....     **/
/** update your SRCSCR dsn with a TSSCFILE entry        **/
/** ISSUE a reinit command                              **/
/*********************************************************/
address ispexec
signal on error name novar
/*********************************************************/
/* the below variables must be set in TSSINI or T$$...   */
/*********************************************************/
ispexec "vget ds5 profile      "
ispexec "vget mode profile     "
ispexec "vget ds3 profile      "
ispexec "vget jcard profile    "
ispexec "vget cfileds profile  "
ispexec "vget srcjcl  profile  "
/*********************************************************/
/* the above variables must be set in TSSINI or T$$...   */
/*********************************************************/
signal on error name seeya
disp = "NEW,CATLG"
if sysdsn(cfileds) = "OK" then disp = "SHR"
outds = ds5
if mode = "BATCH" then outds = ds3
id = sysvar(sysuid)
qx = time()
tnum = substr(qx,1,2)||substr(qx,4,2)
jobname = "TCFL"tnum
queue "//"jobname "JOB" jcard
queue "/*JOBPARM S=*                                           "
queue "//TSSCFILE     EXEC     PGM=TSSCFILE                    "
queue "//PRINT   DD       SYSOUT=*                             "
queue "//OUT     DD       DISP=("disp"),UNIT=SYSALLDA,"
queue "// SPACE=(CYL,(50,10)),DSN="cfileds
queue "//IN      DD       *                                    "
queue "  TSS LIST(ACIDS) DATA(PASSWORD,ALL) TYPE(USER)         "
QUEUE "  TSS LIST(ACIDS) DATA(PASSWORD,ALL) TYPE(DCA)          "
QUEUE "  TSS LIST(ACIDS) DATA(PASSWORD,ALL) TYPE(SCA)          "
QUEUE "  TSS LIST(ACIDS) DATA(PASSWORD,ALL) TYPE(LSCA)         "
QUEUE "  TSS LIST(ACIDS) DATA(PASSWORD,ALL) TYPE(ZCA)          "
queue "  TSS LIST(ACIDS) DATA(PASSWORD,ALL) TYPE(PROFILE)      "
queue "/* "
if srcjcl = "YES" then do
  lines = queued()
  say "lines is "lines
  do x = 1 to lines
    pull thejcl.x
  end
  /***********************************************/
  /** submit the job                            **/
  /***********************************************/
  address tso "alloc ddn(outjcl) dsn("outds") shr reus"
  address mvs "execio "lines" diskw outjcl ( finis stem thejcl.)"
  address tso "free ddn(outjcl)"
  address tso "submit "outds
  secrc = jobname "submitted,jcl saved , output to =>"cfileds"."
  "vput secrc profile"
end
else do
  queue "$$                                                        "
  address tso "SUBMIT * END($$)"
  secrc = jobname "submitted dynamically, output to=>"cfileds"."
  "vput secrc profile"
end
exit 0
seeya: nop
secrc = "tsscfile terminated abnormally, exit line was" sigl
"vput secrc profile"
exit 4
novar: nop
secrc = "tsscfile variable retrieval failed. line was" sigl
"vput secrc profile"
exit 4
