/** rexx                                                 **/
/**********************************************************/
/** removed easyio routine for performance reasons       **/
/**********************************************************/
/** BASELINE BASELINE BASELINE BASELINE BASELINE BASELINE**/
/**********************************************************/
/** EXEC NAME        : TBMSCR                            **/
/** LAST MODIFIED    : 15 APR 98                         **/
/**----------------INPUT REQUIRED -----------------------**/
/** PASSED ARGS        VALID VALUES                      **/
/** INDSN   :  THE FLATFILE ( OR PDS MEM) THAT HAS THE   **/
/**            SCREEN OPTION LIST TO BE PROCESSED.       **/
/** OUTDSN  :  A DSN THAT IS IN THE ISPTLIB CONCATENATION**/
/**            FOR ALL USERS OF THE PANEL SYSTEM.        **/
/** TBNAME  :  THE NAME OF THE OUTPUT TABLE, THIS MUST   **/
/**            MATCH THE NAME SPECIFIED IN TSSINI.       **/
/** AUTHMAX :  THE MAXIMUM AUTH LEVEL OF INPUT COMMAND TO**/
/**            BE INCLUDED IN THE OUTPUT TABLE.          **/
/** STOREOPT:  IF NO THEN I DO NOT STORE THE TABLE       **/
/**                                                      **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00                           **/
/** ISPF VARIABLES        - NONE                         **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES                             **/
/** ISPF ENVIRONMENT   : YES                             **/
/** MY IO ROUTINES     : N/A                             **/
/** EXECS CALLED WITHIN THIS EXEC : NONE                 **/
/**                                                      **/
/**********************************************************/
ARG INDSN OUTDSN TBNAME AUTHMAX STOREOPT
/************************************************************/
TB_PREFIX = "TS5USC"
IF INDSN   = ""   THEN INDSN = "SYS3.TSS.PARMLIB(SCREEN)"
IF INDSN   = "."  THEN INDSN = "SYS3.TSS.PARMLIB(SCREEN)"
IF OUTDSN  = ""   THEN OUTDSN = "SYS3.TSS.TABLE"
IF OUTDSN  = "."  THEN OUTDSN = "SYS3.TSS.TABLE"
IF AUTHMAX = ""   THEN AUTHMAX= 10
IF AUTHMAX = "."  THEN AUTHMAX= 10
IF TBNAME  = "."  THEN TBNAME = TB_PREFIX||AUTHMAX
IF TBNAME  = ""   THEN TBNAME = TB_PREFIX||AUTHMAX
SAY "TSA101D: TBMSCR  PROVIDED VALUES RESOLVED TO:"
SAY "       INDSN   :" INDSN
SAY "       OUTDSN  :" OUTDSN
SAY "       TBNAME  :" TBNAME
SAY "       AUTHMAX :" authmax
SAY "       STOREOPT:" storeopt
IF SUBSTR(INDSN,1,1) /= "'" THEN INDSN = "'"||INDSN||"'"
IF SUBSTR(OUTDSN,1,1) /= "'" THEN OUTDSN = "'"||OUTDSN||"'"
say "TSA101D: TBMSCR indsn resolved to :" indsn
say "TSA101D: TBMSCR outdsn resolved to :" outdsn
"ALLOC DDN(INPUT1) DSN("INDSN") SHR REUS"
SIGNAL OFF ERROR
IF STOREOPT /= "NO" THEN   do
  "ALLOC DDN(OUTPUT) DSN("OUTDSN") SHR REUS"
  ADDRESS ISPEXEC "TBERASE "TBNAME "LIBRARY(OUTPUT)"
  SAY "TSA101D: TBMSCR ERASE    RC IS "RC "FOR " TBNAME
end
ADDRESS ISPEXEC "TBCREATE "TBNAME" KEYS(FNAME) ",
   "NAMES(CMDLINE VARS AUTH ) SHARE"
SAY "TSA101D: TBMSCR TBCREATE RC IS "RC "FOR " TBNAME
ADDRESS ISPEXEC " TBSORT "TBNAME" FIELDS(FNAME,C,A)"
say "TSA101D: TBMSCR TBSORT  RC is " rc
trace off
/*CALL FILEREAD "INPUT1"                 READ DDNAME INPUT1 */
"EXECIO * DISKR INPUT1 ( FINIS STEM HOLD.)"
recnum = hold.0
SAY "TSA101D: TBMSCR RECORDS READ IS" RECNUM
DO X = 1 TO RECNUM
  inline = hold.x
  comment = substr(inline,1,1)
  FNAME   = substr(inline,1,21)
  CMDLINE = substr(inline,23,15)
  VARS    = substr(inline,41,31)
  AUTH    = substr(inline,79,2)
  /* SAY "X IS" X  */
  /* CALL NEXTREC "INPUT1"    */
  IF COMMENT = "*" THEN ITERATE X
  IF AUTH > AUTHMAX THEN ITERATE X
  FNAME = STRIP(FNAME,L)
  IF X = 1 THEN
    ADDRESS ISPEXEC " TBADD "TBNAME " ORDER MULT(250)"
  IF X > 1 THEN
    ADDRESS ISPEXEC " TBMOD "TBNAME " ORDER "
  /* SAY FNAME CMDLINE VARS AUTH */
END
ADDRESS ISPEXEC " TBQUERY "TBNAME" POSITION(ADDED)"
SAY "TSA101D: TBMSCR NUMBER OF ROWS IN TABLE "TBNAME "IS " ADDED
ADDRESS ISPEXEC " TBSORT "TBNAME" FIELDS(FNAME,C,A)"
say "TSA101D: TBMSCR TBSORT  RC is " rc
IF STOREOPT = "NO" THEN
  SAY "TSA101D: TBMSCR TABLE CREATED FOR TEMP USE NO STORE TO BE DONE."
ELSE
  ADDRESS ISPEXEC " TBCLOSE "TBNAME" LIBRARY(OUTPUT)"
SAY "TSA101D: TBMSCR TBCLOSE RC IS " RC
/***********************************************/
"FREE DDN(INPUT1)"
"FREE DDN(OUTPUT)"
EXIT 0
/***********************************************/
/***********************************************/
/***********************************************/
/***********************************************/
/***********************************************/
/***********************************************/
/***********************************************/
/***********************************************/
