/** REXX THE WONDER DOGG                                 **/
/**********************************************************/
/**********************************************************/
/** EXEC NAME        : T51REPPW                          **/
/** LAST MODIFIED    : 11 JUL 94                         **/
/**----------------INPUT REQUIRED -----------------------**/
/** PASSED ARGS        VALID VALUES                      **/
/** ACID    : THE VALID TSS DEFINED ID                   **/
/** NEWPW   : A VALID 1 - 8 CHR NEW PASSWORD             **/
/**                                                      **/
/** ISPF VGETS         VALID VALUES                      **/
/**  ACID IF NOT PASSED AS AN ARG.                       **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00                           **/
/** ISPF VARIABLES        - SECRC                        **/
/**                         MSG2                         **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : NO                              **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC : T51FDIV TSSPWDR      **/
/**                                 T51GDAT              **/
/**********************************************************/
ARG ACID NEWPW
IF ACID = " " THEN ADDRESS ISPEXEC "VGET ACID  PROFILE"
SAY "T51REPPW: HAS CONTROL"
SAY "T51REPPW: CALLING T51FDIV"
ADDRESS TSO "%T51FDIV "ACID
ADDRESS ISPEXEC "VGET MODE PROFILE"
ADDRESS ISPEXEC "VGET SECRC PROFILE"
ADDRESS ISPEXEC "VGET UIDTABLE PROFILE"
ADDRESS ISPEXEC "VGET AUTHNUM PROFILE"
SAY "T51REPPW: BACK FROM  T51FDIV WITH SECRC OF" SECRC
IF SECRC = "AUTHORIZATION FAILED." THEN DO
  SECRC = "FAILED. YOUR TOP SECRET AUTHORIZATION IS INADEQUATE."
  ADDRESS ISPEXEC " VPUT SECRC PROFILE "
  EXIT 00
END
IF SECRC = "CUSTDIV" THEN DO
  IF AUTHNUM < 10 THEN DO
    SECRC = "THAT IS DEFINED AS AN EXTERNAL CUSTOMER. TRY LISTPW"
    ADDRESS ISPEXEC " VPUT SECRC PROFILE "
    EXIT 00
    END
  ELSE DO
   MSG2 = "SUPER (AUTH = 10) INVOKED. PASSWORD NOT NORMALLY CHANGEABLE."
   ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
  END
END
IF (SECRC = "CENTRAL") | (SECRC = "MASTER") THEN DO
  IF AUTHNUM < 10 THEN DO
    SECRC = "YOU ARE NOT AUTHORIZED TO CHANGE THAT ID."
    ADDRESS ISPEXEC " VPUT SECRC PROFILE "
    EXIT 00
    END
  ELSE DO
   MSG2 = "SUPER (AUTH = 10) INVOKED. PASSWORD NOT NORMALLY CHANGEABLE."
   ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
  END
END
ADDRESS TSO "%T51GDAT "ACID" ATTRIBUTES"
ADDRESS ISPEXEC "VGET SECRC PROFILE"
IF INDEX(SECRC,"SUSPEND") /=  0 THEN DO
  MSG2 =  "THIS ID IS SUSPENDED, YOU MUST UNSUSPEND TO USE"
  ADDRESS ISPEXEC " VPUT MSG2 PROFILE "
END
ADDRESS TSO "TSS REP("ACID") PASSWORD("NEWPW",60,EXP)"
IF MODE = "BATCH" THEN DO
  BJCL.1 = " TSS REP("ACID") PASSWORD("NEWPW",60,EXP)"
  ADDRESS MVS "EXECIO 1 DISKW BJCL ( FINIS STEM BJCL.)"
  SECRC = "THE BATCH FILE WAS UPDATED WITH COMMAND TO REPLACE PWORD"
  SIGNAL THEEND
END
ADDRESS TSO "T51GDAT "ACID" PASSWORD PW"
ADDRESS ISPEXEC "VGET SECRC PROFILE"
IF ( SECRC = "$NOTFOUND" ) | ( SECRC = "$BADTSS" )  THEN DO
/*
  MSG2 = "THE IDS PASSWORD IS NOT RETRIEVABLE. CONTACT TECH SUPPORT"
  ADDRESS ISPEXEC " VPUT MSG2 PROFILE "
  */
  secrc = "Reset command issued."
END
ELSE
  secrc = "The ids password is now  = "secrc
THEEND: NOP
ADDRESS ISPEXEC " VPUT SECRC PROFILE "
EXIT 0
