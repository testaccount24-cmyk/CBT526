/** rexx                                                 **/
/**********************************************************/
/** REMOVED ALL LOCATION PROCESSING WITH USE OF AFTER    **/
/** KEYWORD DIR 2 OCT 97                                 **/
/**********************************************************/
/** TO GLOBALLY REPLACE A PROFILE WITH ANOTHER           **/
/**********************************************************/
/** EXEC NAME        : T80GREP                           **/
/** LAST MODIFIED    : 02 OCT 97                         **/
/**********************************************************/
/** TOTALLY REWRITTEN TO IMPROVE SPEED BY 1000%          **/
/**********************************************************/
/**----------------INPUT REQUIRED -----------------------**/
/** PASSED ARGS        VALID VALUES                      **/
/** ACID    : THE ONE TO BE REPLACED                     **/
/** NEWACID : THE ONE TO REPLACE IT WITH                 **/
/** ISPF VGETS         VALID VALUES                      **/
/**                                                      **/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00                           **/
/** ISPF VARIABLES        - SECRC                        **/
/**                         DATAAREA                     **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : NO                              **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC : XXXXXXX XXXXXXX      **/
/**                                                      **/
/**********************************************************/
ARG ACID NEWACID NOTUSED
ADDRESS ISPEXEC
"      VGET DISP PROFILE     "
"      VGET MODE PROFILE     "
"      VGET DS1 PROFILE     "
"      VGET DS2 PROFILE     "
"      VGET DS3 PROFILE     "
"      VGET DS4 PROFILE     "
"      VGET DS5 PROFILE     "
"      VGET DS6 PROFILE     "
MYCODE = 0
TOTAL_RC = 0
ACID = TRANSLATE(ACID," ",".")
NEWACIDS = TRANSLATE(NEWACID," ",".")
IF INDEX(" "NEWACID" "," "ACID" ") \= 0 THEN DO
  msg2  = "TSA241W: Warning - cannot replace an ACID with itself."
  ADDRESS ISPEXEC " VPUT MSG2 PROFILE   "
END
SECRC = "FUNCTION FAILED." ACID "MAY BE INVALID."
"      VPUT SECRC PROFILE   "
SIGNAL ON ERROR NAME CLOSEEM
OUTFILE.0 = 0
STARTOUT: NOP
IF MODE = "ONLINE" THEN DO
  ADDRESS TSO "ALLOC DDN(OUTFILE) DSN("DS3") "DISP" REUS"
  ADDRESS TSO "ALLOC DDN(JCLFILE) DSN("DS6") SHR REUS"
  ADDRESS MVS "EXECIO * DISKR JCLFILE ( FINIS STEM OUTFILE.)"
END
OUTNUM = OUTFILE.0 + 1
/***********/
Q = OUTTRAP(DROPOUT.)
SIGNAL OFF ERROR
DO T1 = 1 TO WORDS(NEWACIDS)
  LISTACID = WORD(NEWACIDS,T1)
  ADDRESS TSO "TSS LIST("LISTACID") DATA(BASIC)"
  TOTAL_RC = TOTAL_RC + RC
  IF TOTAL_RC > 0 THEN BADID = LISTACID
END
IF TOTAL_RC \=0 THEN DO
  MSG2 = "TSA242W: Warning"
  msg2 = msg2 badid " profile was unlistable jcl may not be valid."
  ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
END
Q = OUTTRAP(OUT.)
ADDRESS TSO "%T80PWHO "ACID
ADDRESS ISPEXEC "VGET SECRC PROFILE"
IF SECRC = "$BADTSS" THEN DO
  secrc = "TSA243E: Aborted."
  secrc = secrc acid" profile was unlistable. Check validity."
  ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  MYCODE = 4
  SIGNAL CLOSEEM
END
SIGNAL ON ERROR NAME CLOSEEM
Q = OUTTRAP("OFF")
say "TSA101D: Pwho generated "out.0 "lines of output ."
if out.0 = 0 then do
  secrc = "TSA246E: Unable to find attached ACIDs for replace stream"
  ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  MYCODE = 4
  SIGNAL CLOSEEM
end
AGAIN: NOP
DO X = 1 TO OUT.0
  ID = WORD(OUT.X,1)
  IF ID = "ACCESSORID" THEN ID = WORD(OUT.X,3)
  OUTFILE.OUTNUM ="TSS ADD("ID") PROFILE("NEWACIDS") AFTER("ACID")"
  OUTNUM = OUTNUM + 1
  OUTFILE.OUTNUM ="TSS REM("ID") PROFILE("ACID")"
  OUTNUM = OUTNUM + 1
END
OUTSTART: NOP
OUTFILE.OUTNUM = " "
OUTDD = "OUTFILE"
IF MODE = "BATCH" THEN OUTDD = "BJCL"
ADDRESS MVS "EXECIO * DISKW "OUTDD" ( FINIS STEM OUTFILE.)"
SECRC= "TSA244I: Replace stream generated output in " DS3
IF MODE = "BATCH" THEN
secrc= "TSA245I: Replace stream appended to batchfile."
"ISPEXEC VPUT SECRC PROFILE"
IF MODE = "ONLINE" THEN DO
  ADDRESS TSO "FREE DDN(OUTFILE)"
  ADDRESS TSO "FREE DDN(JCLFILE)"
END
EXIT MYCODE
CLOSEEM: NOP
SAY "TSA101D: T80grep error line is "SIGL
SAY "TSA101D: secrc is "  SECRC
IF MODE = "ONLINE" THEN DO
  ADDRESS TSO "FREE DDN(OUTFILE)"
  ADDRESS TSO "FREE DDN(JCLFILE)"
END
EXIT MYCODE
