/** REXX THE WONDER DOG                                  **/
/**********************************************************/
/** EXEC NAME        : T52INS                            **/
/** LAST MODIFIED    :  9 dec 99                         **/
/**********************************************************/
/** NOW ACCEPTS MULTIPLE PROFS IN STD INVOCATION         **/
/**********************************************************/
/** CHANGED CODE TO FIX ERROR FOR INSERTS AT POSITION    **/
/** GREATER THAN 1 WHEN ID HAS NO PROFILES    ....DR     **/
/**********************************************************/
/** REVAMPED TO USE THE AFTER KEYWORD OF INSERT COMMAND  **/
/** SHOULD BE LOTS FASTER AND EASIER TO MANAGE....DR     **/
/**********************************************************/
/**********************************************************/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00 08 12                     **/
/** ISPF VARIABLES        - SECRC                        **/
/**                         EXITPRM                      **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES                             **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC : %T52FDIV %T52GDAT    **/
/**********************************************************/
ARG ACID PROFILE TARGET DAYS
/************************************************************/
/** PATCH BELOW FOR NORMAL INVOCATION FOR MULTIPLE PROFILES */
/************************************************************/
if target = " " then target = 1
TEMP = PROFILE TARGET DAYS
PROFS = 0
PARSE VAR TEMP TW.1 TW.2 TW.3 TW.4 TW.5 TW.6 TW.7 TW.8 TW.9 TW.10
ALLWORDS = WORDS(TEMP)
IF (ALLWORDS = 3) & ( DATATYPE(TW.2) = "CHAR" ) THEN do
  PROFILE = TW.1||"."||TW.2
  TARGET = DAYS
  DAYS = ""
  say "TSA101D: T52INS reformatted input is :" acid profile target days
END
IF ALLWORDS > 3 THEN DO
  DO COUNT = 1 TO ALLWORDS
    IF DATATYPE(TW.COUNT) = "NUM" THEN PROFS = COUNT - 1
    IF DATATYPE(TW.COUNT) = "NUM" THEN LEAVE
  END
  PROFILE = ""
  DO CNT2 = 1 TO PROFS
    PROFILE = PROFILE||"."||TW.CNT2
  END
  TARGET_WORD = PROFS + 1
  DAYS_WORD = PROFS + 2
  TARGET = WORD(TEMP,TARGET_WORD)
  DAYS   = WORD(TEMP,DAYS_WORD)
  say "TSA101D: T52INS reformatted input is :" acid profile target days
END
/*****/
ACT = "AFTER"
ADDRESS ISPEXEC
PROFILE = TRANSLATE(PROFILE," ",".") /*CHANGE DOTS TO BLANKS */
PROFILE = TRANSLATE(PROFILE," ","_") /*CHANGE _    TO BLANKS */
SYSENV = SYSVAR(SYSENV)
PROF. = " "
PROFHLD = " "
Q = OUTTRAP("OFF")
SAY " "
say "TSA101D: T52INS control received."
say "TSA101D: T52INS profile is" profile
say "TSA101D: T52INS target  is" target
say "TSA101D: T52INS days    is" days
PARSE VAR PROFILE PROF.1 PROF.2 PROF.3 PROF.4 PROF.5
PROFNUM = WORDS(PROFILE)
IF PROFNUM > 5 THEN DO
  secrc = "TSA251E: Failed. Insert function only supports "
  secrc = secrc||"5 profiles at a time"
  say "TSA101D: T52INS:" secrc
  "VPUT SECRC PROFILE"
  EXIT
END
IF DATATYPE(TARGET) ¬= "NUM" THEN DO
   SECRC="FUNCTION ABORTED. LOCATION SPECIFIED IS NOT NUMERIC."
   SECRC= SECRC TARGET
   IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
   EXIT 16
END
/*********************************************************************/
/* NOW I MAKE SURE EACH PROF HE PASSED ME IS ACTUALLY A PROFILE. DIR */
/* NOTE THAT THIS CHECKING IS SKIPPED FOR A BATCH USER.          DIR */
/*********************************************************************/
DO CNT = 1 TO PROFNUM
  SECRC = "PROFILE" /*SET TO PROFILE FOR BATCH DEFAULTS */
  IF SYSENV = "FORE" THEN DO
    say "TSA101D: T52INS environment is foreground"
    say "TSA101D: T52INS I am calling Txxfdiv for data on "prof.cnt
    ADDRESS TSO "%T52FDIV "PROF.CNT
    ADDRESS ISPEXEC " VGET SECRC PROFILE"
  END
  IF SECRC ¬= "PROFILE" THEN DO
    say "TSA101D: T52INS rcode indicates " prof.cnt "is not a profile"
    say "TSA101D: T52INS return code is ===>" secrc
    secrc= "TSA252E: "
    secrc= secrc"Function aborted. "prof.cnt "is not an owned profile."
    IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
    EXIT 08
  END
END
SIGNAL ON ERROR NAME ERRID
say "TSA101D: T52INS testing primary id for existance"
ADDRESS TSO "TSS LIST("ACID") DATA(NAME)"
say "TSA101D: T52INS rcode from test is " rc
SIGNAL OFF ERROR
/*********************************************************************/
/* NOW I REMOVE THE PROFS BEFORE INSERTING THEM JUST IN CASE THIS IS */
/* THE EQUIVALENT OF A MOVE OPERATION...........DIR 19 JUN 91        */
/*********************************************************************/
DO CNT = 1 TO PROFNUM
  say "TSA101D: T52INS removing the stated profiles first !" prof.cnt
  ADDRESS TSO "TSS REM("ACID") PROF("PROF.CNT")"
  IF RC > 0 THEN NOTTHERE = "YES"
END
/*********************************************************************/
/* IF THIS IS THE EQUIVALENT OF A DELETE WE ARE DONE....DIR 19 JUN 91*/
/*********************************************************************/
IF TARGET = 0 THEN DO
  SECRC = PROFILE " REMOVED FROM "ACID", AND NOT REINSERTED."
  IF NOTTHERE = "YES" THEN DO
    say "TSA101D: T52INS non 0 rc received from tss remove command"
    secrc = "TSA253E: Failed. Delete requested but "profile "not"
    secrc =  secrc" removed from ACID" acid"."
  END
  SAY "T52INS:" SECRC
  IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
  EXIT
END
/*                                              */
/*Q = OUTTRAP("OUT.")                           */
/*ADDRESS TSO "TSS LIST("ACID") DATA(BASIC)"    */
Q = OUTTRAP("OFF")
/*********************************************************************/
/* NOW I GET THE PROFILES CURRENTLY ATTACHED TO THE ACID DR 2 MAY96  */
/*********************************************************************/
ADDRESS TSO " %T52GDAT "ACID " PROFILES"
ADDRESS ISPEXEC " VGET SECRC PROFILE"
PROFHLD = SECRC
SAY "T52INS: CURRENT PROFILES ARE : " PROFHLD
TOTWORDS = WORDS(PROFHLD)
IF PROFHLD = "$NOTFOUND" THEN TOTWORDS = 0
IF TARGET > TOTWORDS THEN DO
  TARGET = TOTWORDS + 1
  SAY "T52INS: POSITION PARM OVERRIDDEN. CHANGED TO "TARGET
END
/******************************************************************/
/* WE ISSUE THE COMMAND WITH AN AFTER PARM UNLESS THE FUNCTION IS */
/* TO ADD THE PROFILE AS THE FIRST IN HIS LIST, THEN WE MUST      */
/* USE THE BEFORE PARM. THIS NEXT CODE IS HERE IN CASE TARGET IS  */
/* EQUAL TO POSITION 1. DIR 2 MAY 96                              */
/******************************************************************/
IF TOTWORDS = 0 THEN DO
  IF DAYS =  " " THEN ADDRESS TSO "TSS ADD("ACID") PROF("PROFILE") "
  IF DAYS ¬= " " THEN ADDRESS TSO "TSS ADD("ACID") PROF("PROFILE") FOR("DAYS")"
  SIGNAL SEEYA
END
Q1 = TARGET
IF TARGET = 1 THEN ACT = "BEFORE"
ELSE Q1 = TARGET - 1
IF Q1 = 0 THEN ACT = "BEFORE"
TPROF = WORD(PROFHLD,Q1)
SAY "T52INS: INSERT TGT IS PROFILE " TPROF
SIGNAL ON ERROR NAME ERRXIT
SAY "T52INS: ADDING FOLLOWING PROFILES TO ACID"
SAY "T52INS: PROFILES ===> "PROFILE
SAY "T52INS: CHECKING FOR TEMPORARY TIMESPAN"
IF DAYS =  " " THEN ADDRESS TSO "TSS ADD("ACID") PROF("PROFILE") "ACT"("TPROF")"
ELSE ADDRESS TSO "TSS ADD("ACID") PROF("PROFILE") FOR("DAYS") ",
      ACT"("TPROF")"
SEEYA: NOP
secrc =  "TSA254I:" profile "inserted in "acid" at position" target"."
SAY "T52INS: "SECRC
SAY "  "
IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
EXIT 0

ERRXIT: NOP
SAY "T52INS: ERROR IN LINE:" SIGL
SECRC =  "CATASTROPHIC FAILURE ! PROFILES BELOW NOT ADDED !"
SAY "T52INS: "SECRC
SAY "  "
EXITRC16 = ACID "LOST:" PROFHLD
SAY "T52INS: "EXITRC16
IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
IF SYSENV = "FORE" THEN "ISPEXEC VPUT EXITRC16 PROFILE"
EXIT 1
ERRID: NOP
SECRC =  "INSERT FAILED. CHECK ID!! . MAY NOT EXIST"
SAY "T52INS: "SECRC
SAY "  "
EXITRC12 = ACID "MAY NOT EXIST"
SAY "T52INS: "EXITRC12
IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
IF SYSENV = "FORE" THEN "ISPEXEC VPUT EXITRC12 PROFILE"
EXIT 1
ERRPRF: NOP
SECRC =  "INSERT FAILED. CHECK ID!! . SOME PROFILES MAY NOT BE OWNED!"
SAY "T52INS: "SECRC
SAY "  "
EXITRC12 = ACID "PROFS IN QUESTION:" Y
SAY "T52INS: "EXITRC12
IF SYSENV = "FORE" THEN "ISPEXEC VPUT SECRC PROFILE"
IF SYSENV = "FORE" THEN "ISPEXEC VPUT EXITRC12 PROFILE"
EXIT 1
