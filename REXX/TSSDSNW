/*REXX*/
T = 0
CNT =
ARG DSN DSOUT DISP
IF DISP = " " THEN DISP = "SHR"
DSOUT=STRIP(DSOUT)
ENV = SYSVAR(SYSENV)
SIGNAL ON ERROR NAME SEEYA
SAY "---------------------------------------------------------------"
SAY "TSSDSNW: RUNNING. OUTPUT DSN IS " DSOUT
/*****************************************************************/
/*  THIS EXEC FINDS WHOHAS ACCESS TO A CERTAIN DSN               */
/*  AND SAYS THE IDS TO THE CALLING ROUTINE.                     */
/*  THE DSN PARM IS THE DSN TO BE CHECKED                        */
/*  THE DSOUT  PARM IS THE OUTPUT DSN.                           */
/*****************************************************************/
SAY "TSSDSNW: DSN = "DSN
HEADER1 = " $  THE FOLLOWING IDS ARE AUTHORIZED TO"
HEADER2 = " $$ ACCESS  DSNS       " DSN
PROFILE MSGID WTPMSG
Q1 = OUTTRAP(OUT.)
SIGNAL ON ERROR NAME BADDSN
ADDRESS TSO "TSS WHOHAS DSN("DSN")"
SIGNAL ON ERROR NAME SEEYA
/* QUEUE HEADER1;QUEUE HEADER2 */
Q1 = OUTTRAP(OFF)
SAY "TSSDSNW: WHOHAS RETURNED "OUT.0 "LINES OF OUTPUT"
DO X = 1 TO OUT.0
  T = X + 1
  W = X + 2
  NEXTLINE = OUT.T
  LASTLINE = LINE
  LINE = OUT.X
  IF WORD(LINE,1) = "XAUTH" THEN THE_DSN = WORD(LINE,3)
  /*
  IF THE_DSN = "**" THEN ITERATE X
  IF THE_DSN = "***" THEN ITERATE X
  IF THE_DSN = "****" THEN ITERATE X
  IF THE_DSN = "*****" THEN ITERATE X
  */
  WORD1 = WORD(LINE,1)
  WORD3 = WORD(LINE,3)
  IF WORD(LINE,1) = "XAUTH" THEN ACCESS  = WORD(NEXTLINE,3)
  IF ( WORD1 = "ACTION" ) & ( WORD3 ¬= "FAIL" ) THEN DO
     ACT     = "ACTION("WORD3")"
     DSN.CNT = DSN.CNT ACT
  END
  IF WORD1 = "FAC" THEN DO
    FAC = "FAC("WORD3")"
    DSN.CNT = DSN.CNT FAC
  END
  IF WORD1 = "PRIVPGM" THEN DO
    PGM = "PGM("WORD3")"
    DSN.CNT = DSN.CNT PGM
  END
  IF WORD(LINE,1) = "TSS0300I" THEN LEAVE
  IF WORD(LINE,1) = "TSS300I" THEN LEAVE
  IF WORD(NEXTLINE,1) = "TSS300I" THEN LEAVE
  IF WORD(NEXTLINE,1) = "TSS0300I" THEN LEAVE
  Q = POS("ACID(",LINE)
  IF Q = 0 THEN ITERATE X
  START = Q + 5
  STOP  = LASTPOS(")",LINE)
  STOP  = STOP - START
  ACID = SUBSTR(LINE,START,STOP)
  CNT = CNT + 1
  ACID.CNT = ACID
  DSN.CNT = THE_DSN "  ACCESS("ACCESS")"
  FAC = " "
  ACT = " "
  PGM = " "
  ITERATE X
END
Q = OUTTRAP(PROFS.)
"TSS LIST(ACIDS) TYPE(PROFILE) DATA(NAME)"
DO Y = 1 TO PROFS.0
  PROFS.Y = WORD(PROFS.Y,3)
END
DO Z = 1 TO CNT
  DO Z1 = 1 TO PROFS.0
    IF (ACID.Z = PROFS.Z1) & ((ACID.Z ¬= "TECHDEV"),
       & (ACID.Z ¬= "TELECOM")) THEN DO
      Q = OUTTRAP(OUT.)
      GO = "NO"
      "TSS LIST("ACID.Z") DATA(ACIDS)"
      DO Z2 = 1 TO OUT.0
        PARSE VAR OUT.Z2 W.1 W.2 W.3 W.4 W.5 W.6 W.7 W.8 W.9 W.10
        IF W.1 = "TSS0300I" THEN LEAVE
        IF W.1 = "TSS300I" THEN LEAVE
        IF W.1 = "ACIDS" THEN GO = "YES"
        IF GO <> "YES" THEN ITERATE Z2
        DO Z3 = 1 TO 8
          IF W.Z3 = "TSS0300I" THEN ITERATE Z
          IF W.Z3 = "ACIDS" THEN ITERATE Z3
          IF W.Z3 = "=" THEN ITERATE Z3
          IF W.Z3 = " " THEN ITERATE Z3
          IF W.Z3 = "-ZC" THEN ITERATE Z3
          IF W.Z3 = "-LC" THEN ITERATE Z3
          IF W.Z3 = "-SC" THEN ITERATE Z3
          IF W.Z3 = "-DC" THEN ITERATE Z3
          IF W.Z3 = "-VC" THEN ITERATE Z3
          IF W.Z3 = "(S)" THEN ITERATE Z3
          IF W.Z3 = "(Z)" THEN ITERATE Z3
          IF W.Z3 = "(L)" THEN ITERATE Z3
          IF W.Z3 = "(V)" THEN ITERATE Z3
          IF W.Z3 = "(D)" THEN ITERATE Z3
          W.Z3 = SUBSTR(W.Z3        ,1,8)
          QUEUE " "||W.Z3 "THRU PROFILE " ACID.Z||", TO DSN" DSN.Z
        END
      END
    /* HERE WE MUST PUT EACH ID WITH THAT PROFILE ON THE STACK*/
    ITERATE Z
    END
  END
  QUEUE " "||ACID.Z "THRU USER AUTHORITY,    TO DSN" DSN.Z
END
ACIDNUM = QUEUED()
QUEUE "ZZLAST"
QUEUE ""
/*                                                */
/*DO Z = 1 TO ACIDNUM                             */
/*  PULL ACID                                     */
/*  Q = OUTTRAP(OUT.,2)                           */
/*  "TSS LIST("ACID") DATA(NAME)"                 */
/*  PARSE VAR OUT.1 W.1 W.2 W.3 W.4 W.5 W.6       */
/*  OUTLINE.Z = ACID "=" W.6                      */
/*  Q = OUTTRAP(OFF)                              */
/*END                                             */
SIGNAL OFF ERROR
"ALLOC DDN(OUT) DSN("DSOUT") "DISP" REUS"
"EXECIO 2 DISKW OUT (STEM HEADER FINIS)"
"EXECIO "ACIDNUM" DISKW OUT (FINIS "
"FREE DDN(OUT)"
DELSTACK
SAY "TSSDSNW: HAS COMPLETED WITH " ACIDNUM " LINES OF OUTPUT"
SAY "        IN DATASET " DSOUT
SECRC = "COMPLETED, "ACIDNUM "LINES OF OUTPUT IN "DSOUT
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT
SEEYA: NOP
   SAY "AN ERROR HAS OCCURRED IN LINE = :" SIGL
   SAY "TSSDSNW HAS COMPLETED WITH ERRORS."
SECRC = "ERROR IN LINE # "SIGL" TSSDSNW HAS COMPLETED WITH ERRORS."
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT 1
BADDSN: NOP
   SAY " "
   SAY "THE DATASET YOU HAVE GIVEN ME IS NOT UNDER TOP SECRET CONTROL"
   SAY "OR IS SYNTACTALLY INCORRECT."
   SAY "TOP SECRET MESSAGES ARE BELOW:"
   SAY "******************************"
   SAY OUT.1
   SAY OUT.2
   SAY "******************************"
SECRC = "ERROR IN TSS DATA RETRIEVAL CHECK DEBUG LOG FOR MSGS."
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT 1
