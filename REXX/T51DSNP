/*REXX*/
ARG ACID DSNAME ACCESS DAYS
SIGNAL OFF ERROR
SECRC = "T50DSNP FAILED WITH ERRORS"
D.0 =
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
ADDRESS ISPEXEC "VGET DSNAUDL  PROFILE"
IF DSNAUDL = " " THEN DSNAUDL = "'IMS.PROD.PARMLIB(AUDIT)'"
IF SYSDSN(DSNAUDL) = "OK" THEN DO
  SAY "T50DSNP: DSNAUDL IS" DSNAUDL
  "ALLOC DDN(INPUT) DSN("DSNAUDL") SHR REUS"
  "EXECIO * DISKR INPUT ( FINIS STEM D.)"
  "FREE DDN(INPUT)"
  SAY "T50DSNP: I HAVE "D.0" QUALIFIERS TO AUDIT"
  DO T = 1 TO D.0
    D.T = WORD(D.T,1)
  END
END
Q = OUTTRAP(OUT.)
ACTION = "ACTION(FAIL)"
DAY1 = WORD(DAYS,1)
DAY2 = WORD(DAYS,2)
IF DAYS = "AUDIT" THEN DO
  SAY "T50DSNP: AUDIT VALUE DETECTED WITH NO DAYS LIMITATION."
  DAYS = "0"
  DAY1 = "0"
  ACTION= "ACTION(AUDIT)"
END
IF (DATATYPE(DAY1) ¬= "NUM") & (DAY1 ¬= " " ) THEN DO
  SAY "T50DSNP: DAYS VALUE NOT NUMERIC. EXEC ENDING."
  DAYS = WORD(DAYS,1)
  ACTION= "ACTION(AUDIT)"
  MSG2 = "THE FOLLOWING PARAMETER WAS NOT NUMERIC" DAY1
  ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
END
IF DAY2 = "AUDIT" THEN DO
  SAY "T50DSNP: AUDIT VALUE DETECTED."
  DAYS = WORD(DAYS,1)
  ACTION= "ACTION(AUDIT)"
END
IF (DAY2 ¬= "AUDIT") & (DAY2 ¬= " ") THEN DO
  SAY "T50DSNP: DAY2 VALUE NOT UNDERSTOOD = " DAY2
  DAYS = WORD(DAYS,1)
  ACTION= "ACTION(FAIL)"
  MSG2 = "THE FOLLOWING PARAMETER WAS NOT UNDERSTOOD" DAY2
  ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
END
IF ACCESS ¬= "READ" THEN DO COUNT = 1 TO D.0
  SAY "T50DSNP: COMPARING "DSNAME "TO " D.COUNT
  IF INDEX(DSNAME,D.COUNT) = 1 THEN DO
    SAY "T50DSNP: DSNAUDIT ENTRY MATCH ENTRY" COUNT DSNAME D.COUNT
    ACTION = "ACTION(AUDIT)"
    MSG2 = "THAT DATASET IS AUDITED DAILY, AUDIT RULE INCLUDED"
    IF ACID = "UCC7BAT" THEN DO
      MSG2 = "NO AUDIT RULE ADDED. ID IS THE PRODUCTION JCL ID."
      ACTION = "ACTION(FAIL)"
    END
    ADDRESS ISPEXEC "VPUT MSG2 PROFILE"
  END
END
SAY "T50DSNP: ACTION IS NOW" ACTION ". DAYS VALUE IS " DAYS
RCODE = 0
ADDRESS TSO "TSS WHOOWNS DSN("DSNAME")"
RCODE = RC
SAY "T50DSNP: WHOOWNS RC IS " RCODE
IF RCODE ¬= 0 THEN DO
  SECRC = "THAT DATASET IS NOT UNDER YOUR OWNERSHIP. FUNCTION FAILED."
  ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  SAY OUT.1 OUT.2
  EXIT 04
END
SAY         "TSS LIST("ACID") DATA(NAME)"
ADDRESS TSO "TSS LIST("ACID") DATA(NAME)"
RCODE = RC
SAY "T50DSNP: TSS LIST RC IS " RCODE
IF RCODE ¬= 0 THEN DO
  SECRC = "THAT ID IS NOT OWNED BY YOU OR IS INVALID."
  ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  SAY OUT.1 OUT.2
  EXIT 04
END
ADDRESS TSO
IF ACCESS = "REVOKE" THEN DO
  Q = OUTTRAP(REVOKE.)
  SAY "T50DSNP: REVOKE FUNTION STARTED"
  "TSS REV("ACID") DSN("DSNAME")"
  RCODE = RC
  IF RCODE = 0 THEN
    SECRC = "REVOKE FUNCTION SUCCESSFULL FOR ID "ACID
  ELSE DO
    SECRC = "REVOKE RETURNED NON ZERO RCODE OF " RCODE
    IF WORD(REVOKE.1,1) = "TSS0384E" THEN
      SECRC = "FAILED. CHECK THAT DSN IS IN PROFILE EXACTLY AS ENTERED."
    SAY SECRC "TSS REASON IS :"
    SAY "TSS TEXT REASON IS :"
    SAY REVOKE.1
    SAY REVOKE.2
  END
  ADDRESS ISPEXEC "VPUT SECRC PROFILE"
  EXIT
END
IF DAYS = " " THEN
  "TSS PER("ACID") DSN("DSNAME") ACCESS("ACCESS")" ACTION
ELSE
  "TSS PER("ACID") DSN("DSNAME") ACCESS("ACCESS") FOR("DAYS")" ACTION
RCODE = RC
IF RCODE = 0 THEN
  SECRC = "PERMIT FUNCTION SUCCESSFULL FOR ID "ACID" FOR "DAYS" DAYS."
ELSE
  SECRC = "PERMIT RETURNED NON ZERO RCODE OF "RCODE
ADDRESS ISPEXEC "VPUT SECRC PROFILE"
EXIT
