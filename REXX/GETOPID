/*REXX*/
ARG FNAME LNAME RUNTYPE
/* new code below to remove an entry from the file that is inuse */
if lname /= "REMOVE" then do
  FNAME = SUBSTR(FNAME"   ",1,1)
  LNAME = SUBSTR(LNAME"   ",1,1)
end
memname = substr(fname"   ",1,1)
/** end of new code section.................................  **/
/***************************************************************/
/** Added opid remove function syntax below:                  **/
/** GETOPID DR0 REMOVE LIVE/SIMULATE                          **/
/** Also.....                                                 **/
/** fixed logic which prevented removal of an opid from the   **/
/** last line of a member. aug 9 2006 dir                     **/
/***************************************************************/
/* if runtype is simulate then dont remove anything!! */
/* if runtype is live then provide opid and remove from file ! */
/***************************************************************/
/** this exec retrieves an opid from a pds for a top secret   **/
/** user id creation.. dir 20 jan 93                          **/
/** note: presently the $0,$1,$2 ..etc  members arent checked **/
/**       maybe a future enhancement!!!                       **/
/** this version will check 2 members for an opid (the more   **/
/** members checked the better)    dir 25 jan 93              **/
/***************************************************************/
SECRC = "ERROR IN GETOPID"
ADDRESS ISPEXEC "VGET OPIDDSN PROFILE"
IF SYSDSN(OPIDDSN) /= OK THEN
  OPIDDSN = "SYS4.SPI.OPIDS"
/*OPIDDSN = "CHTS.DAVEPDS" */
BEGIN = FNAME||LNAME||9
HOLD = ""
THEOPID = "#"
INITS = FNAME||LNAME
KILLWORD = 0
KILLLINE = 0
SIGNAL ON ERROR NAME ERRALLOC
"ALLOC DDN(INOPID) DSN('"OPIDDSN"("memname")') SHR REUS"
"EXECIO * DISKR INOPID ( FINIS STEM IN.)"
LEN = LENGTH(NAME)
DO Z = 1 TO IN.0
  IF KILLWORD < > 0 THEN LEAVE
  PREVLINE = INLINE
  NNUM = Z + 1
  NEXT = IN.NNUM
  INLINE = IN.Z
  PARSE VAR INLINE W.1 W.2 W.3 W.4 W.5 W.6 W.7 W.8 W.9 W.10 W.11 W.12,
  W.13 W.14 W.15 W.16 W.17 W.18 W.19 W.20 W.21 W.22 W.23 W.24 W.25
  /* new code below to remove an entry from the file that is inuse */
  if lname = "REMOVE" then  do zz = 1 to 9
    if fname = w.zz then do
      KILLWORD = zz
      theopid = fname
      KILLLINE = Z
      ITERATE Z
    end
  end
  /* end of new code for remove function.......................... */
  IF W.1 = " " THEN ITERATE Z
  Q1 = WORD(INLINE,1)        /* first opid on this line */
  next1 = WORD(NEXT,1)       /* first opid on next line */
  /*
  SAY
  SAY "THIS1 IS " Q1    "............."
  SAY "NEXT1 IS " next1 "............."
  SAY "BEGIN IS " BEGIN "............."
  */
  if substr(next1,1,3) ="IN." then next1 = 'FFFF'x
  IF ( W.1 <= BEGIN ) & ( NEXT1 >> BEGIN ) THEN
    DO WNM1 = 1 TO 25 /* THIS IS THE CORRECT LINE */
      POS3 = SUBSTR(W.WNM1"   ",3,1)
/*    SAY POS3 IS "POS3" */
      IF ( POS(INITS,W.WNM1) = 1 ) & (DATATYPE(POS3) = "NUM") THEN DO
        THEOPID = W.WNM1 /* THE FIRST 2 CHARACTERS MATCH !*/
        KILLWORD = WNM1  /* CLOSE ENOUGH FOR GOVT WORK    */
        KILLLINE = Z
        ITERATE Z
      END
    END
  /* if we get here there were no opids that had a 2 chr match */
  IF ( W.1 <= BEGIN ) & ( NEXT1 >> BEGIN ) THEN
    DO WNUM = 1 TO 25 /* NO HIT SO FAR            */
      IF POS(INITS,W.WNUM) < > 0 THEN DO
        IF KILLWORD < > 0 THEN LEAVE
        THEOPID = W.WNUM /* THE FIRST 2 CHARACTERS MATCH !*/
        KILLWORD = WNUM  /* CLOSE ENOUGH FOR GOVT WORK    */
        KILLLINE = Z
        ITERATE Z
      END
  END
END
/* new code below for remove function.  */
if (killword= 0 & lname = "REMOVE") then do
  secrc = "The opid does not exist, remove function completed."
  signal gotmsg
end
/* end new code                         */
IF KILLWORD = 0 THEN DO
  SAY "INITIAL MEMBER SCAN UNSUCCESSFUL. RETRIEVING A NUMERIC VALUE"
  /*************************************************************/
  /* WE FOUND NO MATCH FOR THE OPID IN THE DESIGNATED MEMBER!! */
  /* SO WE GO TO THE NUMERIC MEMBER AND TAKE THE FIRST NUMBER  */
  /* DIR 20 JAN 93                                             */
  /*************************************************************/
  " FREE DDN(INOPID)" /* IT WASNT IN THERE!! */
  "ALLOC DDN(INOPID) DSN('"OPIDDSN"($NUM)') SHR REUS"
  "EXECIO * DISKR INOPID ( FINIS STEM IN.)" /* READIN FOR DELOPID */
  PARSE VAR IN.1   W.1 W.2 W.3 W.4 W.5 W.6 W.7 W.8 W.9 W.10 W.11 W.12,
  W.13 W.14 W.15 W.16 W.17 W.18 W.19 W.20 W.21 W.22 W.23 W.24 W.25
  THEOPID = W.1
  KILLWORD = 1
  KILLLINE = 1
END
DELOPID: NOP
/***************************************************************/
/* NOTE : YOU MUST HAVE THE STEM VARIABLE IN. FILLED WHEN      */
/* YOU ARRIVE HERE. YOU ALSO MUST HAVE THE DDN INOPID ALLOCATED*/
/* WITH THE PDS MEMBER THAT WILL RECEIVE THE NEW LOOK OPID     */
/* MEMBER.                         DIR 20 JAN 93               */
/***************************************************************/
LINE = IN.KILLLINE
PARSE VAR LINE W.1 W.2 W.3 W.4 W.5 W.6 W.7 W.8 W.9 W.10 W.11 W.12,
W.13 W.14 W.15 W.16 W.17 W.18 W.19 W.20 W.21 W.22 W.23 W.24 W.25
DO X = 1 TO 25
  IF X = KILLWORD THEN ITERATE X
  HOLD = HOLD W.X
END
IN.KILLLINE = HOLD
/*SAY "THE OPID IS " THEOPID  */
/*SAY "KILLLINE IS" KILLLINE  */
/*SAY "KILLWORD IS" KILLWORD  */
/*SAY "NEWLINE IS "           */
/*SAY HOLD                    */
THEOPID = WORD(THEOPID,1)
DATAAREA = THEOPID
"ISPEXEC VPUT DATAAREA PROFILE"
secrc = "The opid would be "theopid". it still exists on the file"
IF RUNTYPE = "LIVE" THEN DO
  "EXECIO * DISKW INOPID ( FINIS STEM IN.)"
  secrc = "The opid is "theopid". it has been removed from file"
END
gotmsg: nop
"FREE DDN(INOPID)"
"ISPEXEC VPUT SECRC PROFILE"
SAY SECRC
EXIT 0
ERRALLOC: NOP
secrc = "Unable to obtain an enqueue on the opid dsn"
"ISPEXEC VPUT SECRC PROFILE"
SAY SECRC
EXIT 2
