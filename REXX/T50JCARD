/** rexx                                                 **/
/**********************************************************/
/** INSERTS A JOBNAME AND MAYBE A USER= CARD INTO A      **/
/** SAMPLE JCL DECK WHICH ALREADY IS SYNTACTICALLY VALID **/
/**********************************************************/
/** EXEC NAME        : T50JCARD                          **/
/** LAST MODIFIED    : 15 MAY 95                         **/
/**----------------INPUT REQUIRED -----------------------**/
/** PASSED ARGS        VALID VALUES                      **/
/** DSN     : THE DSNAME OF THE OUTPUT JCL STREAM        **/
/** JPARM   : "SUPER" OR BLANKS                          **/
/** ISPF VGETS         VALID VALUES                      **/
/**                                                      **/
/** DS6     : THE DSNAME IF THE SAMPLE JOB               **/
/** DISP    : THE OUTPUT FILE ALLOCATION DISPOSITION     **/
/** MSCA    : THE ID TO INCLUDE IN A USER= CARD IF NEEDED**/
/**----------------OUTPUT PRODUCED-----------------------**/
/** VALID CONDITION CODES - 00                           **/
/** ISPF VARIABLES        - NONE                         **/
/**                                                      **/
/**----------------REQUIREMENTS--------------------------**/
/** CAN RUN STANDALONE : YES (NEEDS VGETS)               **/
/** ISPF ENVIRONMENT   : YES                             **/
/** EXECS CALLED WITHIN THIS EXEC :  N/.A                **/
/**                                                      **/
/**********************************************************/
ARG DSN JPARM
ADDRESS ISPEXEC
"ISPEXEC VGET DS6 PROFILE"
"ISPEXEC VGET MSCA PROFILE"
SAY "TSA101D: T50JCARD: IS IN CONTROL WITH PARMS : " DSN JPARM
ADDRESS TSO "ALLOC DDN(JCFILE) DSN("DS6") SHR REUS"
JNAME= JPARM
IF JPARM = '' THEN JNAME = "TSSJOB"
TIME = TIME(NORMAL)
T12 = SUBSTR(TIME,1,2)
T45 = SUBSTR(TIME,4,2)
IF JPARM = "RANDOM" THEN
  JNAME= "TSSJ"T12||T45
ADDRESS TSO "ALLOC DDN(OUTFILE) DSN("DSN") SHR REUS"
ADDRESS MVS "EXECIO * DISKR JCFILE (FINIS STEM LINE.)"
JOBCARD_INSERT = 99
DO Q = 8 TO 1 BY -1 /* LETS FIND WHERE THE JOBCARD ENDS */
  INLINE = LINE.Q
  IF SUBSTR(INLINE,1,3) = "//*" THEN JOBCARD_INSERT = Q - 1
  IF SUBSTR(INLINE,1,2) = "/*" THEN JOBCARD_INSERT = Q - 1
  IF INDEX(INLINE,"EXEC") > 0  THEN JOBCARD_INSERT = Q - 1
END
SAY "TSA101D: T50JCARD JOBCARD ENDS PRIOR TO LINE" JOBCARD_INSERT
IF JPARM = "SUPER" THEN DO
  JNAME= "TSSJ"T12||T45
  LINE.JOBCARD_INSERT= "// "WORD(LINE.JOBCARD_INSERT,2)||",USER="MSCA
  SAY "TSA101D: JCARD ADDITION : " LINE.JOBCARD_INSERT
END
LEN1= LENGTH(LINE.1)
HOLD = SUBSTR(LINE.1,11)
LINE.1= "//"JNAME HOLD
ADDRESS MVS "EXECIO * DISKW OUTFILE (FINIS STEM LINE.)"
ADDRESS TSO " FREE DDN(JCFILE)  "
ADDRESS TSO " FREE DDN(OUTFILE) "
