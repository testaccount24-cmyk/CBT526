/* REXX PGM = FS       - FULL SCREEN VIEW                          */
/*                                                                 */
/* --------------------------------------------------------------- */
/* STORE INPUT IN VARIABLE TSOCMD                                  */
/* --------------------------------------------------------------- */
ARG TSOCMD
IF LENGTH(TSOCMD) = 0 THEN
   DO
     SAY 'FS:   IMS.SYSTEM.CLIST'
     SAY 'FS:   INVALID SYNTAX:'
     SAY 'FS:   SYNTAX IS FS <TSO COMMAND>'
     EXIT
   END
ELSE NOP
X=MSG('OFF')
X=OUTTRAP("OUTLINE.")
/* BELOW 2 LINES OF CODE ADDED TO PREVENT POSSIBLE PROBLEMS    */
/*   THAT CAN OCCUR IF TSOCMD DOES NOT RETURN ANY OUTPUT       */
T = OUTLINE.0 + 1
OUTLINE.T = "                                               "
 WRKDD = "WRK" || RANDOM()               /* GENERATE UNIQUE DDNAME */
"ALLOC F("WRKDD") TRACKS SPACE(60 15) UNIT(VIO) NEW",
  "DSORG(PS) LRECL(132) RECFM(F B) BLKSIZE(0)"
/* --------------------------------------------------------------- */
/* EXECUTE COMMAND                                                 */
/* --------------------------------------------------------------- */
IF INDEX(TSOCMD,"|") > 0 THEN DO
    PARSE VAR TSOCMD CMD1 '|' CMD2
    ""CMD1""
    ""CMD2""
  END
  ELSE
    ""TSOCMD""
IF RC < 0 THEN
   DO
      SAY WORD(TSOCMD,1) 'COMMAND NOT FOUND.'
      SIGNAL ENDD
   END
"EXECIO * DISKW "WRKDD" (FINIS STEM OUTLINE."
ADDRESS ISPEXEC "LMINIT DATAID(DSN1) DDNAME("WRKDD") ENQ(EXCLU)"
ADDRESS ISPEXEC "VIEW DATAID(&DSN1)"
ADDRESS ISPEXEC "LMFREE DATAID(&DSN1)"
ENDD:
"FREE F("WRKDD")"
